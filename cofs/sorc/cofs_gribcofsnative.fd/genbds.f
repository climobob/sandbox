C-----------------------------------------------------------------------
      SUBROUTINE GENBDS (PDS,IGDS,FLD,IU,IMJM,FIRSTIME,RBMAP,FILNAM,
     &                   IPDS4)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    GENBDS      MAKE GRIB MESSAGE
C   PRGMMR: RIVIN            ORG: W/WD20     DATE: 00-18-12
C
C ABSTRACT: USING W3FI72 MAKES A COMPLETE GRIB MESSAGE FROM USER
C   SUPPLIED ARRAY OF FLOATING POINT DATA, PDS, GDS, AND BITMAP.
C
C PROGRAM HISTORY LOG:
C   95-02-23  V.GERALD    GENERATED BDS SECTION WITH CALL TO W3FI72
C
C USAGE:    CALL GENBDS(PDS,IGDS,FLD)
C   INPUT ARGUMENT LIST:
C     PDS   - PRODUCT DEFINITION SECTION
C     IGDS  - GRID DESCRIPTION SECTION
C     FLD   - GRIDDED FIELD TO BE PACKED
C     IBMAP - BITMAP DESCRIPTION
C
C   OUTPUT ARGUMENT LIST:
C     NONE
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:   GTBITS
C
C     LIBRARY:  W3FI72
C     GENERATES BINARY DATA ARRAY WITH BITMAP SECTION USING USER
C     SUPPLIED PDS AND GDS.
C     THE FOLLOWING PARAMETERS :
C           ITYPE = 1 (0) FLAG INDICATING FLOATING POINT (INTEGER) VALUE
C           IBITL = 0 = PROGRAM COMPUTES LENGTH FOR PACKING DATA USING
C                       VARIABLE BIT PACKER (W3FI58)
C                 = OTHER VALUES, CHECK DOCBLOCK OF W3FI73
C           IPFLAG = 1/0 = USER DOES/DOESN'T SUPPLY PDS
C           IGFLAG = 1/0 = USER DOES/DOESN'T SUPPLY IGDS AND IGRID TO CR
C                       GDS
C           IGRID = 255 = USER DEFINED GRID
C           ICOMP = 0/1 = EARTH/GRID ORIENTED WINDS
C           IBFLAG = 0/1 = MAKE BIT MAP FROM USER SUPPLIED DATA / BIT MA
C                    PRE DEFINED
C           IBMAP = BIT MAP ARRAY
C           IBLEN = LENGTH OF BIT MAP
C           IBDSPL = TABEL 11 FLAG INFORMATION :
C                    (1) 0/1 = GRID POINT / SPHERICAL HARMONIC COEFFICIE
C                    (2) 0/1 = SIMPLE/SECOND ORDER PACKING
C                    (3) 0/1 = SAME VALUES AS ITYPE
C                    (4) 0/1 = NO ADDITIONAL FLAGS / MORE FLAGS AT OCTET
C
C    OUTPUTS FROM W3FI72 :
C           NPTS = NO. OF POINTS IN ARRAY FLD
C           KBUF = ENTIRE GRIB MESSAGE
C           LTOT = TOTAL NUMBER OF BYTES OF THIS GRIB MESSAGE
C           JERR = ERROR FLAG = 0 = OK,
C                  OTHER VALUES(1-8) , CHECK DOCBLOCK OF W3FI73
C
C
C REMARKS: NONE
C
C ATTRIBUTES:
C   LANGUAGE: IBM FORTRAN
C   MACHINE:  IBM SP
C
C$$$
C

C     ===========================================
C
C GENERATE GRIB BINARY DATA SECTION FOR COASTAL OCEAN MODEL
C INCLUDE BITMAP FOR LAND POINTS
C       
      INTEGER IGDS(18),ID(25),IFLD(IMJM),IBMAP(IMJM)
      INTEGER  IBDSFL(9)
      LOGICAL FIRSTIME
C
C THE FOLLOWING VARIABLES ARE ONLY NEEDED FOR THE CALL TO W3FI63
C
      REAL DATAFLD(IMJM),RBMAP(IMJM)
      INTEGER KPDS(22),KGDS(20),KPTR(20)
      LOGICAL KBMS(IMJM)
C
      PARAMETER (MNBIT=0,MXBIT=16,LENPDS=28,LENGDS=32)

      CHARACTER*1  KBUF(30+LENPDS+LENGDS+IMJM*(MXBIT+2)/8)
      CHARACTER*1 PDS(28)
C     CHARACTER*1 KBUF(20000)
      CHARACTER*4 ITIME
      CHARACTER*80 FILNAM
      REAL FLD(IMJM)
C
      ITOT = 0
C
C     WRITE(6,*) 'IMJM = ',IMJM
      ITYPE = 0
      IBITL = 0
      IPFLAG = 1
      IGFLAG = 1
      IGRID = 255
      ICOMP = 1
      IBFLAG = 0
      DO 2 I = 1,IMJM
        IF (RBMAP(I).EQ.1.0) THEN
         IBMAP(I) = 1
        ELSE
         IBMAP(I) = 0
        ENDIF
 2    CONTINUE
      IBLEN = IMJM 
      DO 3 I = 1,9
         IBDSFL(I) = 0
 3    CONTINUE
C
C...   PACK GRID INTO GRIB FORMAT...........
      CALL W3FI72 (ITYPE,FLD,IFLD,IBITL,IPFLAG,ID,PDS,IGFLAG,IGRID,
     1             IGDS,ICOMP,IBFLAG,IBMAP,IBLEN,IBDSFL,
     1             NPTS,KBUF,ITOT,JERR)
C
C EXPLICITLY SET BYTE 12 OF KBUF (BYTE 4 OF THE PDS) TO IPDS4.  THIS 
C WILL REFER ALL QUANTITIES TO THE REQUIRED PARAMETER TABLE VERSION OF WHICH    
C TABLE VERSION 1 IS A SUBSET.  THIS IS NEEDED BECAUSE THE W3 
C ROUTINES HARDWIRE THIS VALUE TO 1 YET SOME OF THE OUTPUT VARIABLES
C ARE ONLY DEFINED IN VERSION 2, OR IN LOCALLY DEFINED PARAMETER TABLES.
C
      KBUF(12) =CHAR(IPDS4)
      IF (JERR.NE.0) THEN
          PRINT *,'COMING FROM W3FI72 JERR=',JERR
          STOP
      ENDIF
C
      PRINT *,'AFTER W3FI72 NPTS,ITOT,JERR=',NPTS,',',ITOT,',',JERR
C     WRITE(6,4) (KBUF(I),I=1,ITOT)
 4    FORMAT (' KBUF=',/,(50A1))
C
C UNPACK GRIB MESSAGE FOR DIAGNOSTIC MESSAGE.
C
      CALL W3FI63(KBUF,KPDS,KGDS,KBMS,DATAFLD,KPTR,KRET)
      NPTS = KGDS(2)*KGDS(3)
      FMIN = +1.E32
      FMAX = -1.E32
      DO 40 K=1,NPTS
       IF(KBMS(K)) THEN
         FMIN = AMIN1(FMIN,DATAFLD(K))
         FMAX = AMAX1(FMAX,DATAFLD(K))
       ENDIF
  40  CONTINUE
      WRITE(71,101) (KPDS(K),K=1,22)
 101  FORMAT(22(I5,1X))  
      WRITE(71,*) 'FMIN= ',FMIN,' FMAX= ',FMAX
C     IF (KPDS(5).EQ.80) WRITE(80) DATAFLD
C
C...............................................................
C
      IF (FIRSTIME) THEN
c irivin Jan 19, 2001
c        CALL ASNUNIT(IU,'-s unblocked',IER)
        IF (IER.NE.0) WRITE(6,*) 'GENBDS:  ASNUNIT ERROR FOR GRIB
     1  DATA. IER= ',IER
C        WRITE(FILNAM,220) 
        write (*,*) filnam
        OPEN(IU,FILE=FILNAM,FORM='UNFORMATTED')
        REWIND(IU)
      ENDIF
        CALL WRYTE(IU,ITOT,KBUF)
C
C220  FORMAT('cfsGRB.test')
C
      RETURN
      END
