      SUBROUTINE SPHERE(JGG,AVG,AVN,AVS,JFM)
C     SPATIAL AVERAGE WITH NON-DIMENSIONAL
C     WEIGHTS (FROM OLD AVE) 10/30/79 
      DIMENSION WR(46)
      LOGICAL MPEVEN
      COMMON W(46,50) 
      COMMON/CONST/ 
     1 M,MM,MP,MPP,N, 
     2 DELPHI,DELT,PI,AEARTH,CA,
     3 CP,CO,CD,P00,P0, 
     4 SIGMA1,SIGMA2,XKAPPA,SIG,XLC,
     5 XLF,RHOW,RHOS,DIFF,XKS,
     6 XKI,XIMIN,SC,
     7 ITMAX,XLAM,XLS,PWLF, 
     8 PWLS,CDPW,CDCP,CDLC,CDLS,
     9 PWPS,XKSKI,RSEA,SGKP1,SGKP2, 
     1 ZSGKPA,ZSGKPB,CLRE,SQ1,SQ2 
      DATA MPOLD/0/ 
C     SPHERE COMPUTES NORTHERN HEMISPHERE, SOUTHERN HEMISPHERE, AND 
C     GLOBAL AVERAGES, FOR DATA ON A SPHERE 
C     TABULATED AT EQUALLY SPACED LATITUDES 
C     INCLUDING THE POLES AND EQUATOR.
C     THE AVERAGE IS WEIGHTED ACCORDING TO THE AREA OF EACH LATITUDE
C     BELT. 
C     THE FORMULAS FOR THE WEIGHTS ARE
C     DERIVED FROM THE INTEGRATION
C     OF 0.5*COS(PHI)*DPHI OVER A FINITE INTERVAL.
C     THE WEIGHTS ARE COMPUTED ONLY WHEN MP IS CHANGED. 
      IFMB=0
 50   CONTINUE
      IF (MP.EQ.MPOLD) GO TO 100
      MPEVEN=MOD(MP,2).EQ.0 
      MSEQ = MP/2 
      MZEQ = MSEQ+1 
      MLIM=MPP/2
      PHI = PI/2. 
C     COMPUTES WEIGHTS FOR INTERMEDIATE LATITUDE BELTS
      DO 10 MZ=2,MLIM 
      PHI = PHI-DELPHI
      WR(MZ) = COS(PHI)*SIN(DELPHI/2.)
 10   CONTINUE
C     POLE
      WR(1) = SIN(DELPHI/4.)**2 
C     EQUATOR (WEIGHT/2)
      IF (MPEVEN)WR(MZEQ)=0.5*SIN(DELPHI/2) 
      MPOLD=MP
C     COMPUTE AVERAGES
C     TO IMPROVE ACCURACY, COMPUTATIONS ARE SUMMED FROM THE POLES TO
C     THE EQUATOR.
 100  AVN = 0.0 
      AVS = 0.0 
      IF (IFMB.EQ.0) THEN 
C       TOTAL SURFACE BRANCH
        DO 20 MZ=1,MZEQ 
        AVS=AVS+WR(MZ)*W(MZ,JGG)
        AVN=AVN+WR(MZ)*W(MPP+1-MZ,JGG)
 20     CONTINUE
       ELSEIF (IFMB.GT.0) THEN
C         LAND SURFACE BRANCH 
          DO 21 MZ=1,MZEQ 
          AVS=AVS+WR(MZ)*W(MZ,JGG)*W(MZ,JFM)
          AVN=AVN+WR(MZ)*W(MPP+1-MZ,JGG)*W(MPP+1-MZ,JFM)
 21       CONTINUE
         ELSE 
C         OCEAN SURFACE BRANCH
          DO 22 MZ=1,MZEQ 
          AVS=AVS+WR(MZ)*W(MZ,JGG)*(1.-W(MZ,JFM)) 
          AVN=AVN+WR(MZ)*W(MPP+1-MZ,JGG)*(1.-W(MPP+1-MZ,JFM)) 
 22       CONTINUE
      ENDIF 
      AVG = AVN+AVS 
      AVN = 2*AVN 
      AVS = 2*AVS 
      RETURN
C     LAND ENTRY
      ENTRY SPLAND(JGG,AVG,AVN,AVS,JFM) 
      IFMB=1
      GO TO 50
C     SEA ENTRY 
      ENTRY SPSEA(JGG,AVG,AVN,AVS,JFM)
      IFMB=-1 
      GO TO 50
C     SPLAND,SPSEA GIVE UNNORMALIZED INTEGRALS
C     WHICH MUST BE DIVIDED BY THE GLOBAL LAND OR OCEAN FRACTION
      END 
