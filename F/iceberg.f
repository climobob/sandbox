C     PROGRAM MANMELT
C ********************************************************************
C                                                                    *
C  THIS PROGRAM USES EQUATIONS FROM REPORT # CG-D-62-80 "THEORETICAL *
C  ESTIMATES OF THE VARIOUS MECHANISMS INVOLVED IN ICEBERG DETERIOR- *
C  ATION IN THE OPEN OCEAN ENVIRONMENT" TO MELT ICEBERGS.  THE PRO-  *
C  GRAM USES USER INPUT OF BERG AND ENVIRONMENTAL DATA TO MELT THE   *
C  ICEBERGS. ALL EQUATIONS AND FIGURES IN THE BELOW COMMENTS REFER   *
C  TO THE ABOVE REPORT.                                              *
C                                                                    *
C    THE RELATIVE SPEED (RELSPED) OF THE BERG IS SET TO 0 TO BE ON   *
C  THE CONSERVATIVE SIDE WHEN OUTSIDE OF THE LABRADOR CURRENT AREA.  *
C                                                                    *
C *******************************************************************
C
C  PRIMARY VARIABLES:
C
C   ALAT,ALON   LAST POSITION ICEBERGS MELTED AT
C   BERGSPDE    EASTERN COMPONENT OF ICEBERG DRIFT SPEED
C   BERGSPDN    NORTHERN COMPONENT OF ICEBERG DRIFT SPEED
C   BUOY        BUOYANT CONVECTIVE MELT FACTOR
C   CHARL       CHARACTERISTIC LENGTH OF ICEBERG IN METERS
C   CURX,CURY   CURRENT X AND Y COMPONENTS OF ITS SPEED AND DIRECTION
C   DFTDAY      JULIAN DATE ICEBERG IS MELTED ON
C   FC          FACTOR IN WIND FORCED CONVECTIVE MELT
C   LAT,LON     PRESENT POSITION TO MELT BERGS AT
C   PERMELT     PERCENTAGE OF MELT
C   RELEN       END REMAINING LENGTH OF ICEBERG
C   RELSPED     RELATIVE SPEED OF ICEBERG DRIFT IN CM/S
C   RLEN        STARTING/REMAINING LENGTH OF ICEBERG
C   SEATEMP     SEA SURFACE TEMPERATURE IN DEGREES CELCIUS
C   SIZE        SIZE OF ICEBERG
C   SUN         INSOLATION MELT FACTOR
C   WAVE        WAVE EROSION DETERIORATION FACTOR
C   WDIR        DIRECTION OF WAVE IN DEGREES TRUE
C   WEATHER     WEATHER (OVERCAST OR NOT) MELT FACTOR
C   WHGT        HEIGHT OF WAVE IN FEET
C   WINFO       WIND FORCED CONVECTIVE MELT FACTOR
C   WPER        PERIOD OF WAVE IN SECONDS
C   XAMP        HEIGHT OF WAVE IN CENTIMETERS
C   ZTIME       TIME IN SECONDS TO MELT ICEBERG
C
C ********************************************************************

      REAL*4      ALAT,ALON,RELSPED,RELEN,ZTIME
      CHARACTER   SIZE*1
      INTEGER*2   BNUM,NBERGS, LOG
      LOGICAL     LOG_EXISTS
C
      LOG = 22
      INQUIRE(FILE='MELT.LOG',EXIST=LOG_EXISTS)
      IF(LOG_EXISTS) THEN
         CALL ERASE('MELT.LOG')
      ENDIF
      OPEN (UNIT=LOG,FILE='MELT.LOG')
C
C
C GET NUMBER OF BERGS TO MELT
      WRITE(6,*)
      WRITE(6,*)'        MANUAL MELT PROGRAM'
      WRITE(LOG,*)'   OUTPUT FROM MANUAL MELT PROGRAM'
      WRITE(6,*)
      WRITE(6,*)
      WRITE(6,*)'ENTER THE NUMBER OF BERGS TO MELT (0 TO EXIT): '
      READ(5,10) NBERGS
10    FORMAT(I3)
      WRITE(LOG,*)
      WRITE(LOG,*)'ENTER THE NUMBER OF BERGS TO MELT: ',NBERGS
      IF(NBERGS .EQ. 0) GOTO 995

      DO 800 I=1,NBERGS

        RELSPED=0.0

        WRITE(5,5)I
        WRITE(LOG,5)I

5       FORMAT(/,'-------------------------------------------',/
     -         ' ENTER DATA FOR INPUT # ',I2)
C
C GET THE ENVIRONMENTAL INFORMATION
C ENVIRONMENTAL INFORMATION IS SEA SURFACE TEMPERATURE, SIGNIFICANT
C WAVEHEIGHT AND WAVE PERIOD
C
      WRITE(5,*)
      WRITE(5,*)'ENTER SST (DEGREES & TENTHS  XX.X): '
      READ(6,20) ATEMP
20    FORMAT(F4.1)
      WRITE(LOG,*)
      WRITE(LOG,*)'ENTER SST (DEGREES & TENTHS  XX.X): ',ATEMP

      WRITE(5,*)'ENTER WAVE HEIGHT (FEET  XX.X): '
      READ(6,20) AWAVE
      WRITE(LOG,*)'ENTER WAVE HEIGHT (FEET  XX.X): ',AWAVE

      WRITE(5,*)'ENTER WAVE PERIOD (SECONDS  XX.X): '
      READ(6,20) APER
      WRITE(LOG,*)'ENTER WAVE PERIOD (SECONDS  XX.X): ',APER
C
C GET MELT TIME IN 12 HOUR UNITS.

      WRITE(5,*)'ENTER MELT TIME (12 hour units: 1.5=18hrs, 0.5=6hrs): '
      READ(6,20) ZTIME
      WRITE(LOG,*)'ENTER MELT TIME (12 hour units:): ',ZTIME

C
C GET BERG DATA - BERG#, ALAT, ALONG, SIZE, SHAPE, PERCENT MELT
C
c  only size,shape and percent melt really are needed.
c
      WRITE(5,*)'ENTER BERG NUMBER: '
      READ(6,10) BNUM
      WRITE(LOG,*)'ENTER BERG NUMBER: ',BNUM

      WRITE(5,*)'ENTER BERG LATITUDE (XX.X): '
      READ(6,20) ALAT
      WRITE(LOG,*)'ENTER BERG LATITUDE (XX.X): ',ALAT

      WRITE(5,*)'ENTER BERG LONGITUDE (XX.X): '
      READ(6,20) ALON
      WRITE(LOG,*)'ENTER BERG LONGITUDE (XX.X): ',ALON

      WRITE(5,*)'ENTER BERG SIZE (G,S,M, or L): '
      READ(6,30) SIZE
      WRITE(LOG,*)'ENTER BERG SIZE (G,S,M, or L): ',SIZE
30    FORMAT(A1)

      WRITE(5,*)'ENTER LAST PERCENTAGE MELT (0 if new,  XX.X): '
      READ(6,20) PERMELT
      WRITE(LOG,*)'ENTER LAST PERCENTAGE MELT (XX.X): ',PERMELT
C
C
C THE CHARACTERISTIC LENGTH OF THE BERG IS DETERMINED BY THE SIZE CODE
C USED IN THE ICEPLOT PROGRAM. LENGTHS ARE DEFINED IN IIP OPORDER.
C    THE CHARACTERISTIC LENGTHS USED IN THIS PROGRAM ARE:
C           GROWLER =  16 METERS
C           SMALL   =  60 M
C           MEDIUM  = 122 M
C           RADAR   = 122 M
C           LARGE   = 225 M
C
150     IF(SIZE .EQ. 'G'  .OR.  SIZE .EQ. 'g')THEN
          CHARL=16.0
        ELSEIF(SIZE .EQ. 'S'  .OR.  SIZE .EQ. 's')THEN
          CHARL=60.0
        ELSEIF(SIZE .EQ. 'M'  .OR.  SIZE .EQ. 'm')THEN
          CHARL=122.0
        ELSEIF(SIZE .EQ. 'L'  .OR.  SIZE .EQ. 'l')THEN
          CHARL=225.0
        ENDIF

      IF (PERMELT .GT. 0.0) THEN
        RLEN=CHARL-(CHARL*PERMELT/100.0)
      ELSE
        RLEN=CHARL-(PERMELT*CHARL/100)
      ENDIF
      IF(RLEN.LE.1.)RLEN=1.0

C
C ASSUME CLOUDY CONDITIONS EXIST, SO WEATHER = 1
C IF SUNNY WEATHER WOULD EQUAL 2
C
c  you might want to assume sunny weather for your region.
c
        WEATHER=1
C
C 5 CM/SEC IS ADDED TO THE RELATIVE SPEED BASED ON MEASUREMENTS
C UNDER CALM CONDITIONS
C
c  RELSPED should represent the speed of the tow.
c
        RELSPED=RELSPED+5.0
C
C FC IS A FACTOR IN WIND FORCED CONVECTIVE MELTING DEPENDENT UPON
C RELATIVE SPEED OF THE BERG AND THE LENGTH OF THE BERG
C
      IF(RELSPED .GT. 25.0) THEN
        FC=(.660-(.154*DLOG10(DBLE(RLEN) )))*(RELSPED-25)+(.934-(.202*DLOG10
     -     (DBLE(RLEN) )))*25
      ELSE
        FC=(.934-(.202*DLOG10(DBLE(RLEN) )))*RELSPED
      ENDIF
C
C THE TEMPERATURE OF THE ICE SURFACE IS ASSUMED TO BE -1 DEGREE C
C ANY SEA SURFACE TEMPERATURE FROM THE DATA FILE BELOW -1 IS SET TO -1.
C OTHERWISE THE BERG WOULD GET BIGGER. WITH IT SET TO A MINIMUM OF
C -1 THE BERG WILL JUST REMAIN THE SAME SIZE.
C
        IF (ATEMP .LT. -1.0) ATEMP = -1.0
C
C BUOYANT CONVECTIVE MELTING USING EQUATION # 52
C
        BUOY=(.274*(2.78*(ATEMP+1)+.47*((ATEMP+1)
     -  **2)))*ZTIME*.5/100.
C
C WIND FORCED CONVECTIVE MELTING.  THIS IS DERIVED FROM FIGURE #25
C
        WINFO=FC*(ATEMP+1)*ZTIME*.5/100
C
C CONVERT WAVEHEIGHT FROM FEET TO CENTIMETERS
C
        XAMP=AWAVE*30.5
        IF(XAMP.LE.0.0)XAMP=1.0
C
C WAVE EROSION BASED UPON EQUATION # 77b
C
        IF (APER .LE. 0.0) APER=5.0
        WAVE=(XAMP*.000146*((2./XAMP)**0.2)*24.*ZTIME*.5*3600./APER
     -  )*((ATEMP+1)/100.)
C
C INSOLATION MELTING BASED UPON FIGURE # 22
C
        SUN=2.0*WEATHER*ZTIME*.5/100.
        XMELT=BUOY+WINFO+WAVE+SUN
        PERMELT=PERMELT+(XMELT*100./CHARL)
        IF(PERMELT.GT.999)PERMELT=999
        RELEN=CHARL-(PERMELT*CHARL/100.)
        IF(RELEN.LE.0)RELEN=0.0
C
C
C OUTPUT RESULTS
C
        WRITE(5,50)BNUM,ALAT,ALON,PERMELT
        WRITE(LOG,50)BNUM,ALAT,ALON,PERMELT
50      FORMAT(/'BERG # ',I3,' AT POSITION ',F4.1,'N ',F4.1,'W'/
     -         ,'HAS MELTED ',F5.1,'%',/)
C
C
800   CONTINUE
C
995   CLOSE (LOG)
      CALL EXIT(0)
      END

