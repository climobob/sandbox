      SUBROUTINE SPHERV
     1(IDIR,GRIDU,GRIDV,WAVED,WAVEZ,WORK,COSL,IMAX,JMAX,MAXWV,IROMB)
C
C  SPHERICAL TRANSFORM BETWEEN
C  A VECTOR FIELD (I.E. THE HORIZONTAL WIND) IN GRID SPACE AND
C  ITS DIVERGENCE AND CURL (I.E. VORTICITY) IN SPECTRAL SPACE
C
C  IDIR.GT.0  GRID TO WAVE:  FIELDS WAVED,WAVEZ ARE OUTPUT
C       IDIR= 1 ...... NORMAL TRANSFORM (GAUSSIAN GRID)
C           101 ...... AS IDIR= 1 BUT FOR EQUIDISTANT LAT/LON GRID
C  IDIR.LT.0  WAVE TO GRID:  FIELDS GRIDU,GRIDV ARE OUTPUT
C       IDIR= -1 ...... NORMAL TRANSFORM (GAUSSIAN GRID)
C           -101 ...... AS IDIR=-1 BUT FOR EQUIDISTANT LAT/LON GRID
C
C  GRIDU ARE THE GRIDDED IMAX*JMAX X-COMPONENTS OF THE VECTOR FIELD
C
C  GRIDV ARE THE GRIDDED IMAX*JMAX Y-COMPONENTS OF THE VECTOR FIELD
C
C  WAVED ARE THE 2*NMMAX SPECTRAL COEFFICIENTS OF THE DIVERGENCE
C  WAVED = 1./(RERTH*COSL) * (D(GRIDU)/DLAMBDA + D(COSL*GRIDV)/DPHI)
C
C  WAVEZ ARE THE 2*NMMAX SPECTRAL COEFFICIENTS OF THE CURL
C  WAVEZ = 1./(RERTH*COSL) * (D(GRIDV)/DLAMBDA - D(COSL*GRIDU)/DPHI)
C
C  WORK IS WORKSPACE OF AT LEAST MAX(2*NMMAX,IMAX*JMAX) WORDS
C
C  COSL ARE THE JMAX COSINES OF LATITUDE
C
C  IROMB = 1 FOR RHOMBOIDAL TRUNCATION:  NMMAX=(MAXWV+1)*(MAXWV+1)
C        = 0 FOR TRIANGULAR TRUNCATION:  NMMAX=(MAXWV+1)*(MAXWV+2)/2
C
C  REQUIRES TWO-WAY VERSION OF SUBROUTINE SPHERT TO PERFORM TRANSFORMS
C
      DIMENSION GRIDU(IMAX,JMAX), GRIDV(IMAX,JMAX)
      DIMENSION WAVED(2,1), WAVEZ(2,1)
      DIMENSION WORK(1)
      DIMENSION COSL(JMAX)
      DATA SQRT2/1.414214/, RERTH/6.3712E6/
C***********************************************************************
C  GRID TO WAVE SECTION
      IF (IDIR.GT.0) THEN
 
C  SCALE GRID FIELDS
        DO 10 J=1,JMAX
        DO 10 I=1,IMAX
          IF (COSL(J).GT.0.) THEN
            GRIDU(I,J) = GRIDU(I,J)*(RERTH*COSL(J))
            GRIDV(I,J) = GRIDV(I,J)*(RERTH*COSL(J))
          ENDIF
   10   CONTINUE
 
C  SAVE ZONAL WAVENUMBER
        NM = 0
        DO 20 M=0,MAXWV
        DO 20 N=M,MAXWV+IROMB*M
          NM = NM + 1
          WORK(NM) = M
   20   CONTINUE
 
C  TRANSFORM TO WAVESPACE, RESPECTIVELY TAKING X AND Y DERIVATIVES
        CALL SPHERT(IDIR+2,GRIDU,WAVED,-1,WORK,IMAX,JMAX,MAXWV,IROMB)
        CALL SPHERT(IDIR+3,GRIDV,WAVEZ,0,0,IMAX,JMAX,MAXWV,IROMB)
 
C  COMPUTE DIVERGENCE
        NM = 0
        DO 30 M=0,MAXWV
        DO 30 N=M,MAXWV+IROMB*M
          NM = NM + 1
          WAVED(1,NM) = (WAVED(1,NM) + WAVEZ(1,NM))/RERTH**2
          WAVED(2,NM) = (WAVED(2,NM) + WAVEZ(2,NM))/RERTH**2
   30   CONTINUE
 
C  TRANSFORM TO WAVESPACE, RESPECTIVELY TAKING X AND Y DERIVATIVES
        CALL SPHERT(IDIR+2,GRIDV,WAVEZ,-1,WORK,IMAX,JMAX,MAXWV,IROMB)
        CALL SPHERT(IDIR+3,GRIDU,WORK,0,0,IMAX,JMAX,MAXWV,IROMB)
 
C  COMPUTE CURL
        NM = 0
        DO 40 M=0,MAXWV
        DO 40 N=M,MAXWV+IROMB*M
          NM = NM + 1
          WAVEZ(1,NM) = (WAVEZ(1,NM) - WORK(2*NM-1))/RERTH**2
          WAVEZ(2,NM) = (WAVEZ(2,NM) - WORK(2*NM))/RERTH**2
   40   CONTINUE
 
C  UNSCALE GRID FIELDS
        DO 50 J=1,JMAX
        DO 50 I=1,IMAX
          IF (COSL(J).GT.0.) THEN
            GRIDU(I,J) = GRIDU(I,J)/(RERTH*COSL(J))
            GRIDV(I,J) = GRIDV(I,J)/(RERTH*COSL(J))
          ENDIF
   50   CONTINUE
C***********************************************************************
C  WAVE TO GRID SECTION
      ELSE IF (IDIR.LT.0) THEN
 
C  SCALE WAVE FIELDS
        NM = 0
        DO 60 M=0,MAXWV
        DO 60 N=M,MAXWV+IROMB*M
          RNN1A2 = -N*(N+1)/RERTH**2
          NM = NM + 1
          IF (N.GT.0)  THEN
            WAVED(1,NM) = WAVED(1,NM)/RNN1A2
            WAVEZ(1,NM) = WAVEZ(1,NM)/RNN1A2
            WAVED(2,NM) = WAVED(2,NM)/RNN1A2
            WAVEZ(2,NM) = WAVEZ(2,NM)/RNN1A2
          ENDIF
   60   CONTINUE
 
C  SAVE ZONAL WAVENUMBER
        NM = 0
        DO 70 M=0,MAXWV
        DO 70 N=M,MAXWV+IROMB*M
          NM = NM + 1
          WORK(NM) = M
   70   CONTINUE
 
C  TRANSFORM TO GRIDSPACE, RESPECTIVELY TAKING X AND Y DERIVATIVES
        CALL SPHERT(IDIR,GRIDU,WAVED,-1,WORK,IMAX,JMAX,MAXWV,IROMB)
        CALL SPHERT(IDIR-1,GRIDV,WAVEZ,0,0,IMAX,JMAX,MAXWV,IROMB)
 
C  COMPUTE X-COMPONENT OF THE VECTOR FIELD
        DO 80 J=1,JMAX
        DO 80 I=1,IMAX
          IF (COSL(J).GT.0.) THEN
            GRIDU(I,J) = (GRIDU(I,J) + GRIDV(I,J))/(RERTH*COSL(J))
          ENDIF
   80   CONTINUE
 
C  TRANSFORM TO GRIDSPACE, RESPECTIVELY TAKING X AND Y DERIVATIVES
        CALL SPHERT(IDIR,GRIDV,WAVEZ,-1,WORK,IMAX,JMAX,MAXWV,IROMB)
        CALL SPHERT(IDIR-1,WORK,WAVED,0,0,IMAX,JMAX,MAXWV,IROMB)
 
C  COMPUTE Y-COMPONENT OF THE VECTOR FIELD
        IJ = 0
        DO 90 J=1,JMAX
        DO 90 I=1,IMAX
        IJ = IJ + 1
          IF (COSL(J).GT.0.) THEN
            GRIDV(I,J) = (GRIDV(I,J) - WORK(IJ))/(RERTH*COSL(J))
          ENDIF
   90   CONTINUE
 
C  UNSCALE WAVE FIELDS
        NM = 0
        DO 100 M=0,MAXWV
        DO 100 N=M,MAXWV+IROMB*M
          RNN1A2 = -N*(N+1)/RERTH**2
          NM = NM + 1
          IF (N.GT.0)  THEN
            WAVED(1,NM) = WAVED(1,NM)*RNN1A2
            WAVEZ(1,NM) = WAVEZ(1,NM)*RNN1A2
            WAVED(2,NM) = WAVED(2,NM)*RNN1A2
            WAVEZ(2,NM) = WAVEZ(2,NM)*RNN1A2
          ENDIF
  100   CONTINUE
 
      ENDIF
C***********************************************************************
      RETURN
      END
