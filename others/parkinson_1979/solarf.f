      SUBROUTINE SOLARF(M,N,IT,JT)
C
      IMPLICIT none
      INTEGER M, N, IT, JT

      REAL TEMP, FLON, DEW, VP, CLDD, DRAGW
      REAL RLAT, RLONG, DLAT, DLONG
      INTEGER NSTEP
      REAL COSZ, STIME, TIME, TSFLUX, XLAT(42,31), XLON(42,31)
      REAL SOLCST

      INTEGER LLL, I, J, LSTEP, L1, MT
      REAL RNSTEP, GTIME, X1, SFLUX, Q0
      REAL RN, FISR

      COMMON /C1/ TEMP,FLON,DEW  ,VP,CLDD,DRAGW
      COMMON /C6/ RLAT,RLONG,DLAT,DLONG,NSTEP,
     1COSZ,STIME,TIME,TSFLUX,XLAT,XLON
      DATA SOLCST/81180.0/
C  TSFLUX IS THE TOTAL SOLAR RADIATION AT THE SURFACE
C  AT LOCATION X,Y FOR THE 24 HOURS CENTERED AT
C  TIMESTEP N+5 OF MONTH M.
C  UNITS OF TSFLUX - J M-2 S-1
C  NEED X,Y,VP,CLDD,     M,N,NSTEP
C  LLL=THE TIMESTEP (IN THE YEAR) ABOUT WHICH THE CALCULATIONS ARE
C  CENTERED.
      LLL=N+5+90*(M-1)
      IF (LLL  .GE.1081) LLL  =LLL  -1080
C  OBTAIN TIME ABOUT WHICH THE 24HRS ARE CENTERED
      L1=30*LLL  /NSTEP
      RNSTEP=FLOAT(NSTEP)
      RN=FLOAT(LLL  )-RNSTEP*FLOAT(L1)/30.0
C  GTIME =GREENWICH SOLAR TIME IN HOURS.
      GTIME=RN*720.0/RNSTEP
      TIME=GTIME+DLONG/15.0
      MT=IFIX(TIME/24)
      X1=FLOAT(MT)
      TIME=TIME-X1 *24.0
C  CALCULATE TSFLUX THROUGH 2 1-HR. AND 11 2-HR. SUMMANDS
      SFLUX=0.0
      DO 110 I=1,11
      J=2*(I-6)
      STIME=TIME+FLOAT(J)
      LSTEP=LLL
      IF (STIME.GE.0.0) GO TO 50
      LSTEP=LLL-3
      STIME=STIME+24.0
      GO TO 60
   50 CONTINUE
      IF (STIME.LE.24.0) GO TO 60
      LSTEP=LLL+3
      STIME=STIME-24.0
   60 CONTINUE
C  CALLING GEOMR OBTAINS THE COSINE OF THE SOLAR ZENITH ANGLE, COSZ.
      CALL GEOMR(LSTEP,IT,JT)
      Q0=SOLCST*COSZ
C  (FISR IS THE MOMENTARY FLUX OF INCOMING
C  SOLAR RADIATION.)
      FISR=Q0*(COSZ)/(((COSZ+2.7)*VP/1.0E5    )+1.085*COSZ+0.100)
      SFLUX=SFLUX+FISR*120.0
  110 CONTINUE
      STIME=TIME-11.5
      LSTEP=LLL
      IF (STIME.GE.0.0) GO TO 120
      LSTEP=LLL-3
      STIME = STIME +24.0
  120 CONTINUE
      CALL GEOMR(LSTEP,IT,JT)
      Q0=SOLCST*COSZ
      FISR=Q0*(COSZ)/(((COSZ+2.7)*VP/1.0E5    )+1.085*COSZ+0.100)
      SFLUX=SFLUX+FISR*60.0
      STIME=TIME+11.5
      LSTEP=LLL
      IF (STIME.LE.24.0) GO TO 130
      LSTEP=LLL+3
      STIME=STIME-24.0
  130 CONTINUE
      CALL GEOMR(LSTEP,IT,JT)
      Q0=SOLCST*COSZ
      FISR=Q0*(COSZ)/(((COSZ+2.7)*VP/1.0E5    )+1.085*COSZ+0.100)
      TSFLUX=(SFLUX+FISR*60.0)*(1.0-0.6*CLDD**3)
      TSFLUX=TSFLUX/86400.0
      RETURN
      END
