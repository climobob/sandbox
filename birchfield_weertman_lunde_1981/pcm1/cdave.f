      SUBROUTINE CDAVE(NS)
C     SUBROUTINE TO SAVE SELECTED FIELDS BY 
C     WRITING TO A CODED FIXED FORMAT FILE  9/13/82 
C     CALL FOR EVERY TIME STEP TO BE SAVED
C     REVISED TO USE AN ARRAY FOR AVERAGING, RATHER THAN
C       WRITING OFF TO TAPE. 10/6/83  BG
C     AVERAGES COMPUTED ALSO COMPUTED HERE NOW, INSTEAD OF
C       INVOKING THREE SUBROUTINES TO DO AVERAGING. 
C     AVERAGES ARE WRITTEN OUT TO TAPE 20.
C     MODIFIED TO SEND THREE DIGIT INTEGERS TO TAPE 21 FOR
C     LATER TRANSMISSION TO LOCY
      COMMON W(46,50) 
      COMMON/AV3COM/J,JON,DUMA(5) 
      COMMON/CONST/M,MM,MP,MPP,N,DUM(42)
      COMMON/LARGE/NFLD,JFLD(12,2)
      COMMON/MINOR/NMINOR,KFLD(26)
      COMMON/WSPACE/THWRK(46) 
      DIMENSION AVEG(46,5)
C     OUTAV AND LOCAV ARE THE DATA GROUPED BY FIELD, THEN MONTH.
C       OUTAV IS THE FULL DATA SET, WHICH IS SAVED ON TAPE (11/11/83) 
C       LOCAV IS THE DATA FOR PLOTTING AT LOCY. 
      DIMENSION OUTAV(46,12,5)
      DIMENSION LOCAV(46,12,5)
      DIMENSION IN(30),SUB(30)
C     IN SPECIFIES THE POWER OF TEN TO MULTIPLY THE VALUE BEING 
C      PROCESSED BY.  SUB GIVES THE NUMBER TO SUBTRACT.  THIS IS
C      DONE TO GIVE THE TRANSMITTED DATA MORE SIGNIFICANCE
C      FOR TRANSMISSION TO LOCY.
C 
C     COMMON BLOCKS THAT SPECIFY WHAT FIELDS TO OUTPUT
C     INITIALIZE BELOW IN A DATA STATEMENT OR READ IN MAIN PROG 
C     NCDOUT IS THE NUMBER OF FIELDS TO OUTPUT
C     IUCD IS THE UNIT THAT THE AVERAGES ARE OUTPUT TO. 
C     NMERGE IS THE NUMBER OF STEPS TO AVERAGE TOGETHER 
C     ICDOUT IS THE 1ST SUBCRIPT OF THE POINTER TO
C        EACH FIELD IN JFLD OR KFLD 
C     JCDOUT =1  JFLD( ,2) POINTERS,/LARGE/,CURRENT STEP
C            =2  KFLD( ) POINTERS,/MINOR/ 
C     THETA BAR AND THETA HAT ARE DENOTED AS IF THEY WERE FIELDS WITH 
C     POINTERS IN /LARGE/ WITH
C     SUBSCRIPTS BEYOND NFLD=12.
C     I.E. ICDOUT=13,JCDOUT=1 DENOTES THETA BAR 
C          ICDOUT=14,JCDOUT=1 DENOTED THETA HAT 
      COMMON/NCDOUT/NCDOUT,IUCD,NMERGE
      COMMON/ICDOUT/ICDOUT(30)
      COMMON/JCDOUT/JCDOUT(30)
C     DATA STATMENTS HERE TO INITIALIZE /NCDOUT/,/ICDOUT/,/JCDOUT/
C     AVERAGE 5 STEPS TOGETHER
      DATA NMERGE/5/
C     AND OUTPUT THETA BAR,THETA HAT,HL,XIO,AND INSOLATION
      DATA NCDOUT/5/
      DATA IN/1,1,3,2,26*0/ 
      DATA SUB/273.,0.,0.,0.,26*0./ 
      DATA IUCD/20/ 
      DATA (ICDOUT(L),L=1,5)/13,14,8,10,4/
      DATA (JCDOUT(L),L=1,5)/1,1,1,1,2/ 
C     SF TO CONVERT THE DATA TO A THREE SIGNIFICANT DIGIT INTEGER 
      ISF(X,I)=INT(10**IN(I)*(X-SUB(I))+.5) 
C 
C 
      IF(NCDOUT.LE.0)RETURN 
      ITRIP=ITRIP+1 
      XNS=NS/NMERGE+XNS 
      DO 100 L=1,NCDOUT 
      IF(JCDOUT(L).GT.1)GO TO 12
C     FIELDS WITH /LARGE/POINTERS 
C     TEST FOR THETA BAR,THETA HAT REQUESTS 
      IF(ICDOUT(L).LE.NFLD) THEN
        JJ=JFLD(ICDOUT(L),2)
        GO TO 14
C       THETA BAR,THETA HAT 
       ELSEIF(ICDOUT(L).EQ.NFLD+1) THEN 
C         THETA BAR 
          DO 101 MZ=1,MPP 
          THWRK(MZ)=0.5*(W(MZ,JFLD(1,2))+W(MZ,JFLD(2,2))) 
          AVEG(MZ,L)=AVEG(MZ,L)+THWRK(MZ)/NMERGE
 101       CONTINUE 
         ELSEIF(ICDOUT(L).EQ.NFLD+2) THEN 
C         THETA HAT 
          DO 102 MZ=1,MPP 
          THWRK(MZ)=0.5*(W(MZ,JFLD(1,2))-W(MZ,JFLD(2,2))) 
          AVEG(MZ,L)=AVEG(MZ,L)+THWRK(MZ)/NMERGE
 102      CONTINUE
      ENDIF 
      GO TO 100 
C     FIELDS WITH /MINOR/ POINTERS
 12   JJ=KFLD(ICDOUT(L))
 14   CONTINUE
      DO 110 MZ=1,MPP 
        AVEG(MZ,L)=AVEG(MZ,L)+W(MZ,JJ)/NMERGE 
 110  CONTINUE
 100  CONTINUE
      IF (ITRIP.EQ.NMERGE) THEN 
        DO 200 L=1,NMERGE 
          DO 205 II=1,46
C         WRITE THE MONTHLY AVERAGE INTO THE APPROPRIATE PART OF OUTAV. 
            OUTAV(II,MONTH,L)=AVEG(II,L)
 205      CONTINUE
 200    CONTINUE
C     RESET VALUES TO ZERO TO BEGIN NEXT MONTH. BG
        XNS=0.0 
        MONTH=MONTH+1 
        ITRIP=0 
        DO 210 I=1,NMERGE 
        DO 210 JJ=1,MPP 
          AVEG(JJ,I)=0.0
 210    CONTINUE
      ENDIF 
      RETURN
      ENTRY CDCMP 
C     WRITE OUT DATA IN LONG FORM TO TAPE 20. 
      DO 900 K=1,5
      DO 900 J=1,12 
      WRITE (IUCD,500) (OUTAV(I,J,K),I=1,46)
 900  CONTINUE
 500  FORMAT (4G18.12)
      DO 1000 K=1,5 
      DO 1000 J=1,12
      DO 1000 I=1,46
        LOCAV(I,J,K)=ISF(OUTAV(I,J,K),K)
 1000 CONTINUE
      IUCD=IUCD+1 
C     WRITE THE LOCY DATA ON THE FILE AFTER THE UNPROCESSED DATA. 
      DO 1100 K=1,5 
      DO 1100 J=1,12
      IF(K.EQ.3) THEN 
        WRITE (IUCD,501) 0,0,0,0,0,0,0,(LOCAV(I,J,K),I=8,46)
C       WRITE OUT ZEROES FOR SNOW HEIGHT OVER THE SOUTH POLE. 
C         OTHERWISE THE DATA ARE TOO FAR APART IN MAGNITUDE. 11/19/83 
       ELSE 
        WRITE (IUCD,501) (LOCAV(I,J,K),I=1,46)
      ENDIF 
 1100 CONTINUE
 501  FORMAT (1X,16I4)
      RETURN
      ENTRY CDSET 
C     ENTRY POINT TO SET UP VALUES FOR CD 
      XNS=0.0 
      MONTH=1 
      ITRIP=0 
      DO 300 I=1,NMERGE 
      DO 300 JJ=1,MPP 
        AVEG(JJ,I)=0.0
 300  CONTINUE
      RETURN
      END 
