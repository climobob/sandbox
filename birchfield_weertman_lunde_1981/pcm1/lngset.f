      SUBROUTINE LNGSET 
C     TSL/PS  COMPUTATION CALLED 5/3/79 
C     ADD DHLZ ACC.RATE COMPUTATIONS    11/23/79
C     ADD HCONV, HELV FIELD CA. 7/23/80 
C     JALBL, JALBO VARIABLE ALBEDO PARAMETERIZATION 1/16/81 
C     INITIALIZE MINOR FIELDS THAT VARY ONLY ON LONG TIME SCALES
  
      COMMON/CNCNT/IPSOLD, IPSCNT, MOLD 
      COMMON W(46, 50)
      COMMON/LARGE/NFLD,
     1  JTH1P, JTH2P, JTSLP, JTSOP, JTXLP, JTXOP, 
     2  JTMP, JHLP, JHOP, JXIOP, JQMLP, JQMOP,
     3  JTH1, JTH2, JTSL, JTSO, JTXL, JTXO, 
     4  JTM, JHL, JHO, JXIO, JQML, JQMO 
C     LARGE CONTAINS THE POINTERS TO
C     THE PAIRS OF  'MAJOR' FIELDS IN W . VARIABLE
C     NAMES MADE BY ADDING J TO THE NAMES USED FOR THE DATA.
      EQUIVALENCE(JFLD(1, 1), JTH1P)
      DIMENSION JFLD(12, 2) 
  
      COMMON/CONST/ 
     1  M, MM, MP, MPP, N,
     2  DELPHI, DELT, PI, AEARTH, CA, 
     3  CP, CO, CD, P00, P0,
     4  SIGMA1, SIGMA2, XKAPPA, SIG, XLC, 
     5  XLF, RHOW, RHOS, DIFF, XKS, 
     6  XKI, XIMIN, SC, 
     7  ITMAX, XLAM, XLS, PWLF, 
     8  PWLS, CDPW, CDCP, CDLC, CDLS, 
     9  PWPS, XKSKI, RSEA, SGKP1, SGKP2,
     1  ZSGKPA, ZSGKPB, CLRE, SQ1, SQ2
      COMMON/MINOR/NMINOR,
     1JFM, JRSN, JPS, JSP, JSPP, JCZB, JCZBP, 
     2JQ1P, JQ2P, JEXHT 
     4, JFSM(3, 2), JTEQ, JXIEQ, JRSNL, JRSOUT, JRLOUT
     5,JDHLZ,JHELV,JALV,JALBL,JALBO 
  
C     ALBEDO PARAMETERIZATION 1/16/81 
      COMMON/ALCOM/ALP0L,ALP0O,ALP1,ALP2,ALP3 
  
      DATA ALP0L,ALP0O,ALP1,ALP2,ALP3/.14,.07,.55,.75,.55/
C     BARE LAND=ALP0L,OCEAN=ALP0O,BARE SEA ICE=ALP1,NEW SNOW=ALP2,OLD 
C       SNOW=ALP3.
C     TALLY OF PS COMPUTATIONS 4/10/81. 
  
      IPSCNT=IPSCNT+1 
      DO 10 MZ=1,MPP
        W(MZ,JPS)=P00 
        W(MZ,JEXHT)=1.0 
        CALL TSIT(W(MZ,JHELV),W(MZ,JTH1),W(MZ,JTH2),
     1  W(MZ,JTSL),W(MZ,JTSO),W(MZ,JPS),W(MZ,JEXHT))
        W(MZ,JRSNL)=W(MZ,JRSN)
        PZERO=950.
        AZERO=2.75E5
        C=35.7
        IF(W(MZ,JPS).LT.PZERO) THEN 
          W(MZ,JRSNL)=W(MZ,JRSNL)*
     1    (W(MZ,JPS)/PZERO)** 
     2    (AZERO/((W(MZ,JTSL)-C)**2)) 
        ENDIF 
C       ALBEDO PARAMETERIZATION 1/16/81 
        IF(W(MZ,JHL).NE.0.0) THEN 
          W(MZ,JALBL)=0.5*(ALP2+ALP3) 
         ELSE 
          W(MZ,JALBL)=ALP0L 
        ENDIF 
        IF(W(MZ,JXIO).EQ.0.0) THEN
          W(MZ,JALBO)=ALP0O 
         ELSEIF (W(MZ,JHO).EQ.0.0) THEN 
           W(MZ,JALBO)=ALP1 
          ELSE
            W(MZ,JALBO)=.5*(ALP2+ALP3)
        ENDIF 
 10   CONTINUE
  
  
      RETURN
      END 
