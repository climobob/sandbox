      SUBROUTINE DONE 
C     QUADRATIC APPROX TO RAD PARAMETERS  5/22/79 
C     DOUBLE ITERATION FOR CENTERED Q 6/11/79 
C     SMOOTHING SUB ADDED    6/12/79
C     CDLS TO CDLC IN 84,94       MOD   8/28/79 
C     BOUND INPUT TO QUADRATIC  APPROX.   10/18/79
C     ADD DHLZ ACC.RATE COMPUTATIONS    11/23/79
C     SET S1,S2,S4 TO 3/4 OF THEIR ORIGINAL VALUES FOR TUNING EXPERIMENT
C     MIXING RATIO AFFECT ON LAND SNOW FALL 12/1/79 
C     POTENTIAL ACC SNOW HALF THE YEAR 1 DEC 79 
C     MIXED LAYER DEPTH TRIPLED TO 120 M
C     COMPUTE A VARIABLE LAND ALBEDO 12/11/80 
C     JALBL,JALBO VARIABLE ALBEDO PARAMETERIZATION 1/16/81
C     TWO-DIMENSIONAL CASE ONE TIME STEP
  
      COMMON W(46,50) 
  
      COMMON/CONST/ 
     1  M, MM, MP, MPP, N,
     2  DELPHI, DELT, PI, AEARTH, CA, 
     3  CP, CO, CD, P00, P0,
     4  SIGMA1, SIGMA2, XKAPPA, SIG, XLC, 
     5  XLF, RHOW, RHOS, DIFF, XKS, 
     6  XKI, XIMIN, SC, 
     7  ITMAX, XLAM, XLS, PWLF, 
     8  PWLS, CDPW, CDCP, CDLC, CDLS, 
     9  PWPS, XKSKI, RSEA, SGKP1, SGKP2,
     1  ZSGKPA, ZSGKPB, CLRE, SQ1, SQ2
  
      COMMON/ZCONST/Z1, Z2, DZ, DZI, PPSKZ1, PPSKZ2, GEE, SIPERMB, RGEE 
C     COMMON/ZCONST/INITIALIZED IN REVISED CON 4/27/81
C     Z1=ALOG(SIGMA1) 
C     Z2=ALOG(SIGMA2) 
C     DZ=Z1-Z2
C     DZI=1./DZ 
C     PPSKZ1=(SIGMA1*(P0/P00))**XKAPPA*DZI
C     PPSKZ2=(SIGMA2*(P0/P00))**XKAPPA*DZI
C     ACCELERATION OF GRAVITY (METERS/SEC)
C     GEE=9.8 
C     CONVERSION FACTOR BETWEEN SI AND MILLIBAR PRESSURE UNITS
C     SIPERMB=1.E2
C     NOTE R=XKAPPA*CP (R IS GAS LAW CONSTANT)
C     RGEE=XKAPPA*CP/GEE
C     IN /CONST/
C     CO=SIPERMB*(CP/(2.*GEE))
  
      COMMON/THCOM/THCR(10) 
      COMMON/NOTE/NT(3) 
      COMMON/MIXCOM/CMIX
      EXTERNAL FLAND
      EXTERNAL FSEA 
  
      COMMON/LARGE/NFLD,
     1  JTH1P, JTH2P, JTSLP, JTSOP, JTXLP, JTXOP, 
     2  JTMP, JHLP, JHOP, JXIOP, JQMLP, JQMOP,
     3  JTH1, JTH2, JTSL, JTSO, JTXL, JTXO, 
     4  JTM, JHL, JHO, JXIO, JQML, JQMO 
C     LARGE CONTAINS THE POINTERS TO
C     THE PAIRS OF  'MAJOR' FIELDS IN W . VARIABLE
C     NAMES MADE BY ADDING J TO THE NAMES USED FOR THE DATA.
      EQUIVALENCE(JFLD(1, 1), JTH1P)
      DIMENSION JFLD(12, 2) 
  
      COMMON/MINOR/NMINOR,
     1  JFM, JRSN, JPS, JSP, JSPP, JCZB, JCZBP, 
     2  JQ1P, JQ2P
     3  , JEXHT 
     4  , JFSM(3, 2), JTEQ, JXIEQ, JRSNL, JRSOUT, JRLOUT
     5  , JDHLZ 
     6  , JHELV 
     7  , JALV
     8  , JALBL, JALBO
C     MINOR POINTS TO OTHER FIELDS IN W 
C     NMINOR IS THE NUMBER OF POINTERS IN MINOR.
  
      COMMON/AV3COM/JAVE, JAVEON, MZERO, MPREF, LHLS, LHOS, LXIOS 
  
C     ALBEDO PARAMETERIZATION 1/16/81 
      COMMON/ALCOM/ALP0L, ALP0O, ALP1, ALP2, ALP3 
  
  
C     STATEMENT FUNCTIONS FOR REVISED TSL, TSO 4/27/81
      TSLAND(TH1, TH2, ZL)=PPSKZ1*TH1*(ZL-Z2)+PPSKZ2*TH2*(Z1-ZL)
      TSSEA(TH1, TH2)=RSEA*(TH2*ZSGKPA+TH1*ZSGKPB)
  
C     QUADRATIC APPROXIMATIONS TO RAD PAPAMETERS
      BOUND(X, A, B)=AMIN1(B, AMAX1(A, X))
      QUAD(X,A,B,C)=(A+X*(B+X*C)) 
      QUAE(X,A,B,C)=QUAD(BOUND(X,0.1  ,0.7  ),A,B,C)
      QUAF(X,A,B,C)=QUAD(BOUND(X,-30. ,70.  ),A,B,C)
      ZSB(A,B,C)=QUAE(COSZBP,A,B,C) 
      ZSG(A,B,C)=QUAE(COSZBP,A,B,C)*THBAR 
      ZSD(A,B,C)=QUAE(COSZBP,A,B,C)*THHAT 
      ZLA(A,B,C)=QUAF(THBAR,A,B,C)
      ZLB(A,B,C)=QUAF(THBAR,A,B,C)*THHAT
      ZLC(A,B,C)=QUAF(THBAR,A,B,C)*DELTA
  
C     MIXING RATIO AFFECT ON LAND SNOW FALL 12/1/79 
C     R(T)=RSNL*(RMIXL(T,PS)/RMIXL(273.,P0))
      R(T)=RSNL 
  
C     STATEMENT FNC FOR REV L1,L2,L3  10/27/82
      DL1(X)=7.404486E-4+1.6060524E-4*X+3.6066968E-7*X**2 
      DL2(X)=3.422841E-4+1.144654E-4*X+8.074271E-7*X**2 
      DL3(X)=0.001402187+0.000905076*X+0.840E-6*X**2
C     END OF STATEMENT FNC FOR REV L CALC 10/27/82
  
  
      DO 10 MZ=1,MPP
      TH1P=W(MZ,JTH1P)
      TH2P=W(MZ,JTH2P)
      TSLP=W(MZ,JTSLP)
      TSOP=W(MZ,JTSOP)
      TXLP=W(MZ,JTXLP)
      TXOP=W(MZ,JTXOP)
      HLP =W(MZ,JHLP )
      HOP =W(MZ,JHOP )
      XIOP=W(MZ,JXIOP)
      FM  =W(MZ,JFM  )
      RSNWFL=W(MZ,JRSN) 
      RSNL=W(MZ,JRSNL)
      PS  =W(MZ,JPS  )
      SPP =W(MZ,JSPP )
      COSZBP=W(MZ,JCZBP)
C     SHORT WAVE RADIATION
  
C     ************* SECA ************************ 
C     4/24/79 PS/P0 MIX ADDED TO Q S AND MIXING RATIOS    ATL 
C     QUADRATIC APPROX TO RAD PARAMETERS  5/22/79 
C     SHORT-WAVE RADIATION CALCULATIONS 
      THBAR = ((TH1P + TH2P)/2. - 300.0)
      THHAT = (TH1P - TH2P)/2.
  
C     ALBEDO PARAMETERIZATION 1/16/81 
      ALL = W(MZ,JALBL) 
      ALO = W(MZ,JALBO) 
  
C     RADIATION TERMS S3,S4,S5,S6 
      S3 = ZSB(0.810428571428,0.131666666669, -0.0976190476219) 
     1  + ZSG(-0.126571428571E-2,0.166904761905E-2,-0.123809523809E-2)
     2  + ZSD(0.0957142857143E-3,-0.164285714286E-3,0.142857142857E-3)
      S4 = ZSB(0.179,0.03777380952382,-0.029761904762)
     1  + ZSG(-0.477142857143E-3,0.319047619047E-3,-0.238095238095E-3)
     2  + ZSD(0.194285714286E-3,-0.184523809523E-3,0.154761904761E-3) 
      S4 = .87449*S4
      IF(MZ.LE.7) S4 = S4*1.20
      S5 = ZSB(0.467714285714,0.15880952381,-0.111904761906)
     1  + ZSG(-0.167E-2,0.0577380952376E-2,-0.0297619047614E-2) 
     2  + ZSD(0.156428571429E-2,-0.168809523809E-2,0.13333333333333E-2) 
      S6 = ZSB(0.466571428571,0.128571428572,-0.1)
     1  + ZSG(-0.165571428571E-2,0.0388095238094E-2,-0.0261904761904E-2)
     2  + ZSD(0.152285714286E-2,-0.100952380952E-2,0.0761904761906E-2)
  
C     INITIALIZE Q1P AND Q2P TOTALS 
C     AND COMPUTE QSW TERMS 
C     WE CHANGE 1.-(S1+S2) TO .9143 IN FOLLOW  (SEP 80) 
      S1PS2=0.08570 
      IF(MZ.LE.7) S1PS2=S1PS2*1.2 
      Q1P=SPP*(.9143-S3)
C     CONVERT Q TO S FOR CRANK-NICKELSON ITERATION
      Q1P=(FM+(1.-FM))*Q1P
      Q2L=SPP*(S3-S4-S5+ALL*(S5-S6))
      Q2O=SPP*(S3-S4-S5+ALO*(S5-S6))
C     CONVERT Q TO S
      Q2P=(FM*Q2L+(1.-FM)*Q2O)
C     SHORTWAVE OUT FOR PLANETARY ALBEDO
C     WE NOW SET S1+S2=.08570, IN THE FOLLOWING STATEMENT.
      W(MZ,JRSOUT)=SPP*(S1PS2+S4+(FM*ALL+(1.-FM)*ALO)*S6) 
C     INITIALIZE LONGWAVE OUT PARTIAL SUM 
      W(MZ,JRLOUT)=0.0
C     LONG-WAVE RADIATION LAND
C     ************* SECB ************************ 
      F=FM
C     4/24/79 PS/P0 MIX ADDED TO Q S AND MIXING RATIOS    ATL 
C     QUADRATIC APPROX TO RAD PARAMETERS  5/22/79 
C     CALL TWICE-FOR LAND, AND FOR WATER
C     CDLS TO CDLC IN 84,94       MOD   8/28/79 
C     LONG-WAVE RADIATION, SENSIBLE 
C     HEAT, AND LATENT HEAT CALCULATION.
      IF(F.LE.0.) GO TO 122 
C     (COMPUTE Q1P Q2P TERMS ONLY IF THEIR WEIGHT 
C     IS NON-ZERO)
C     L1, L2, L3 LONG-WAVE RAD. TERMS 
      THBAR = (TH1P + TH2P)/2. - 273. 
      THHAT = (TH1P - TH2P)/2.
      DELTA = TXLP - TSLP 
      XL1 = ZLA(1.683912475E2,2.072981931,8.959035194E-4) 
     1  + ZLB(-1.390077537,-5.788031980E-3,1.593910690E-4)
     2  + ZLC(1.034289128,-1.798161720E-3,-.1409863919E-3)
      XL2 = ZLA(1.761887947E2,1.540933984,-1.024410895E-2)
     1  + ZLB(-2.626480570,-9.377568840E-3,1.830390060E-4)
     2+ZLC(1.288038604,-2.251261722E-3,-1.749250542E-4) 
      XL3 =ZLA(2.004079689E2,4.047099851,3.713532325E-2)
     1+ZLB(-3.351779412,-6.016330886E-2,-4.451933616E-4)
  
      DP=P0-PS
      IF(DP.GT.0.0) THEN
        XL3=XL3*(1.-DL3(DP))
        XL2=XL2*(1.-DL2(DP))
        XL1=XL1*(1.-DL1(DP))
      ENDIF 
  
C     SENSIBLE HEAT 
      QSH = CDCP * DELTA
C     LATENT HEAT 
      IF(HLP.LE.0.) THEN
C       NO ICE/SNOW COVER 
C       LIQUID TO VAPOR LATENT HEAT 
        QLH = CDLC * (RMIXL(TXLP,PS) - RMIXL(TSLP,PS) * 0.8)
       ELSE 
C       ICE/SNOW PRESENT
C       SUBLIMATION LATENT HEAT 
        QLH = CDLC * (RMIXI(TXLP,PS) - RMIXI(TSLP,PS) * 0.8)
      ENDIF 
C     ADDITIONAL LATENT HEAT FROM SNOW FALL 
C     NOT DEPENDENT OF HLP, ONLY ON TSLP
      IF(TSLP.LE.273.)
C     BELOW FREEZING-SNOW 
C     MIXING RATIO AFFECT ON LAND SNOW FALL 12/1/79 
     1  QLH=QLH+PWLF*R(TSLP)
  
C     NOTE PS HERE MAY BE PS OR P0 DEPENDING ON CALLING SEQ.
C     QLW TERM LAYER 1
      QLW1=(XL2-XL1)
C     QLW TERM LAYER 2
      TT = TXLP * TXLP
      QLW2=(SIG*TT*TT-XL2-XL3)
  
C     CONVERT Q TO S FOR C-N ITERATION AND ADD TO PARTIAL SUM 
      Q1P=Q1P+F*QLW1
C     SENSIBLE AND LATENT HEAT (WITH SNOW FALL) 
      Q2P=Q2P+F*(QLW2+QSH+QLH)
C     LONGWAVE OUT PARTIAL SUM
      W(MZ,JRLOUT)=W(MZ,JRLOUT)+XL1*F 
  
  
 122  CONTINUE
  
C     LONG WAVE RADIATION OCEAN 
C     ************* SECC ************************ 
      F=1.-FM 
C     4/24/79 PS/P0 MIX ADDED TO Q S AND MIXING RATIOS    ATL 
C     QUADRATIC APPROX TO RAD PARAMETERS  5/22/79 
C     CDLS TO CDLC IN 84,94       MOD   8/28/79 
C     CALL TWICE-FOR LAND, AND FOR WATER
C     LONG-WAVE RADIATION, SENSIBLE 
C     HEAT, AND LATENT HEAT CALCULATION.
  
  
      IF(F.LE.0.) GO TO 123 
C     (COMPUTE Q1P Q2P TERMS ONLY IF THEIR WEIGHT 
C     IS NON-ZERO)
C     L1, L2, L3 LONG-WAVE RAD. TERMS 
      THBAR = (TH1P + TH2P)/2. - 273. 
      THHAT = (TH1P - TH2P)/2.
      DELTA = TXOP - TSOP 
      XL1 = ZLA(1.683912475E2, 2.072981931, 8.959035194E-4) 
     1  + ZLB(-1.390077537, -5.788031980E-3, 1.593910690E-4)
     2  + ZLC(1.034289128, -1.798161720E-3, -.1409863919E-3)
      XL2 = ZLA(1.761887947E2, 1.540933984, -1.024410895E-2)
     1  + ZLB(-2.626480570, -9.377568840E-3, 1.830390060E-4)
     2  + ZLC(1.288038604, -2.251261722E-3, -1.749250542E-4)
      XL3 = ZLA(2.004079689E2, 4.047099851, 3.713532325E-2) 
     1  + ZLB(-3.351779412, -6.016330886E-2, -4.451933616E-4) 
  
C     SENSIBLE HEAT 
      QSH = CDCP * DELTA
C     LATENT HEAT 
      IF(XIOP.LE.0.) THEN 
C       NO ICE/SNOW COVER 
C       LIQUID TO VAPOR LATENT HEAT 
        QLH = CDLC * (RMIXL(TXOP,P0) - RMIXL(TSOP,P0) * 0.8)
       ELSE 
C       ICE/SNOW PRESENT
C       SUBLIMATION LATENT HEAT 
        QLH = CDLC * (RMIXI(TXOP,P0) - RMIXI(TSOP,P0) * 0.8)
      ENDIF 
C     ADDITIONAL LATENT HEAT FROM SNOW FALL 
C     NOT DEPENDENT OF XIOP, ONLY ON TSOP 
      IF(TSOP.LE.273.)
C     BELOW FREEZING-SNOW 
     1  QLH = QLH + PWLF * RSNWFL 
C     NOTE PS HERE MAY BE PS OR P0 DEPENDING ON CALLING SEQ.
C     QLW TERM LAYER 1
      QLW1=(XL2-XL1)
C     QLW TERM LAYER 2
      TT = TXOP * TXOP
      QLW2=(SIG*TT*TT-XL2-XL3)
C     CONVERT Q TO S FOR C-N ITERATION AND ADD TO PARTIAL SUM 
      Q1P=Q1P+F*QLW1
C     SENSIBLE AND LATENT HEAT (WITH SNOW FALL) 
      Q2P=Q2P+F*(QLW2+QSH+QLH)
C     LONGWAVE OUT
      W(MZ,JRLOUT)=W(MZ,JRLOUT)+XL1*F 
  
  
 123  CONTINUE
  
C     ******************************************* 
      W(MZ,JQ1P )=Q1P 
      W(MZ,JQ2P )=Q2P 
      CALL NOTEMZ(MZ) 
 10   CONTINUE
  
  
      CALL SMFUN(1,MP,JQ1P) 
      CALL SMFUN(2,MP,JQ2P) 
  
C     CALL TWO LAYER CN SOLVER 4/4/81 
      CALL CNBLK(JTH1,JTH2,JTH1P,JTH2P,JQ1P,JQ2P) 
      W(1,JTH1)=W(2,JTH1) 
      W(1,JTH2)=W(2,JTH2) 
      W(MPP,JTH1)=W(MP,JTH1)
      W(MPP,JTH2)=W(MP,JTH2)
  
      DO 30 MZ=1,MPP
      TH1 =W(MZ,JTH1 )
      TH1P=W(MZ,JTH1P)
      TH2 =W(MZ,JTH2 )
      TH2P=W(MZ,JTH2P)
      TSLP=W(MZ,JTSLP)
      TSOP=W(MZ,JTSOP)
      TXLP=W(MZ,JTXLP)
      TXOP=W(MZ,JTXOP)
      TMP =W(MZ,JTMP )
      HLP =W(MZ,JHLP )
      HOP =W(MZ,JHOP )
      XIOP=W(MZ,JXIOP)
      QMLP=W(MZ,JQMLP)
      QMOP=W(MZ,JQMOP)
      RSNWFL=W(MZ,JRSN) 
      RSNL=W(MZ,JRSNL)
      PS  =W(MZ,JPS  )
      SP  =W(MZ,JSP  )
      SPP =W(MZ,JSPP )
      EXHT=W(MZ,JEXHT)
  
  
C     ************* SECE23 ********************** 
C     COMPUTE CONVECTIVE ADJUSTMENT 
C     AND SURFACE TEMP TSL AND TSO
C     4/27/79 MIN VALUE OF THC ENFORCED 
C     CONVECTIVE ADJUSTMENT AND SURFACE TEMP TS.
C     RSEA IS (P0/P00)**XKAPPA
C     COMPUTE TBAR ON BASIS OF ENTIRE COLUMN DOWN TO SL, IE IGNORE
C     PLATEAU. THIS AMOUNTS TO USING TBAR OF OCEAN COLUMN.
C     MAKE CORRECTION BY EQUATING RLAND TO RSEA. 10/6/81
C     NEW LAPSE RATE CRITICAL CAL JUL 16 82 
  
      RLAND = (PS/P00)**XKAPPA
      RLAND = RSEA
      TBAR  = 0.5 * RLAND * (SGKP1 * TH1 + SGKP2 * TH2) 
      THHAT = 0.5 * (TH1 - TH2) 
      THC =TABLE(TBAR,THCR,10,200.,10.) 
      THC =AMAX1(THCR(1),THC) 
      IF(THHAT.LT.THC) THEN 
C       ADJUST THETAS TO MOIST ADIABATS 
        A1 = RLAND * SGKP1
        A2 = RLAND * SGKP2
        A12 = (A1 + A2) * 0.5 
        TH1 = (TBAR + A2*THC)/A12 
        TH2 = (TBAR - A1*THC)/A12 
        NT(2)=NT(2)+1 
      ENDIF 
C     COMPUTE SURFACE TEMP (SEC E3) 
      TSO=TSSEA(TH1,TH2)
      TSL=TSLAND(TH1,TH2,EXHT)
C     EXHT IS NOW ZL=ALOG (PSL/PSO) 
C     ******************************************* 
  
C     RADIATION PARAMETERS FOR SECF AND SECG
C     CURRENT STEP
C     THIS IS ACTUALLY COSZB
      COSZBP= W(MZ,JCZB ) 
      THB   = 0.5*(TH1+TH2) 
      THHAT = 0.5*(TH1-TH2) 
      THBAR = THB-300.
      S5 = ZSB(0.467714285714,0.15880952381,-0.111904761906)
     1  + ZSG(-0.167E-2,0.0577380952376E-2,-0.0297619047614E-2) 
     2  + ZSD(0.156428571429E-2,-0.168809523809E-2,0.13333333333333E-2) 
      THBAR = THB-273.
      XL3   = ZLA(2.004079689E2,4.047099851,3.713532325E-2) 
     1  + ZLB(-3.351779412,-6.016330886E-2,-4.451933616E-4) 
C     LAST STEP 
      COSZBP = W(MZ,JCZBP)
      THB    = 0.5*(TH1P+TH2P)
      THHAT  = 0.5*(TH1P-TH2P)
      THBAR  = THB-300. 
      S5P = ZSB(.467714285714,0.15880952381,-0.111904761906)
     1  +ZSG(-0.167E-2,0.0577380952376E-2,-0.0297619047614E-2)
     2  +ZSD(0.156428571429E-2,-0.168809523809E-2,0.13333333333333E-2)
      THBAR= THB-273. 
      XL3P = ZLA(2.004079689E2,4.047099851,3.713532325E-2)
     1  + ZLB(-3.351779412,-6.016330886E-2,-4.451933616E-4) 
  
C     SEA SURFACE 
C     ************* SECF ************************ 
C 
C     COMPUTES ONE STEP OF THE EQUATIONS
C     FOR THE SEA-ATMOSPHERE BOUNDARY 
C     FOR MS + 1 = MZ 
C 
C     ICE THICKNESS 
      DXIP = 0.0
C 
C     IS MIXED LAYER BELOW FREEZING AT N-1
      IF (TMP.LE.273.) THEN 
C 
C       FREEZING DUE TO CONDUCTION OF HEAT TO ATM.
        DXIP = DXIP + QCO(TXOP,HOP,XIOP)/PWLF 
C 
        IF (HOP.LE.0.) THEN 
C 
C         MELTING OF ICE
          DXIP = DXIP - QMOP/PWLF 
C        SUBLIMATION OF ICE 
     1- CDPW * (RMIXI(TXOP,P0) - RMIXI(TSOP,P0) * 0.8)
        ENDIF 
      ENDIF 
C 
C     EXTRAPOLATE 
      XIO = XIOP + DXIP * DELT
C 
      IF (XIO.LT.0.) XIO=0.0
C 
C     SNOW FALL RATE
      SN = 0.0
      IF(TSOP.LE.273.) SN = RSNWFL
C 
C     SNOW THICKNESS HO 
C 
      IF (XIO.LE.0.) THEN 
        HO = 0.0
       ELSE 
C       DERIVATIVE OF HO
        DHP = SN
        IF (HOP.GT.0.)
C       MELTING OF SNOW 
     1     DHP = DHP - QMOP/PWLF
C       SUBLIMATION OF SNOW 
     1  -CDPW * (RMIXI(TXOP,P0) - RMIXI(TSOP,P0) * 0.8) 
C       EXTRAPOLATION 
        HO = HOP + DHP * DELT 
        IF (HO.LT.0.) HO=0.0
      ENDIF 
  
C     ALBEDO PARAMETERIZATION 1/16/81 
      ALP=W(MZ,JALBO) 
      IF (XIO.EQ.0.0) THEN
        W(MZ,JALBO)=ALP0O 
       ELSEIF (HO.EQ.0.0) THEN
         W(MZ,JALBO)=ALP1 
        ELSEIF (HO.GT.HOP) THEN 
          W(MZ,JALBO)=ALP2
         ELSE 
           W(MZ,JALBO)=ALP3 
      ENDIF 
      AL=W(MZ,JALBO)
  
C     SURFACE TEMP. TXO 
  
      IF(XIO.LE.0.0) THEN 
C       NO ICE AT CURRENT STEP
C       (DERIVATIVE AT N-1) * CO
        TT = TXOP * TXOP
        DTX = (1. - ALP) * S5P * SPP + XL3P - SIG * TT * TT 
C       SENSIBLE HEAT FLUX
     1   - CDCP * (TXOP - TSOP) 
C       LATENT HEAT FLUX OF LIQUID TO VAPOR 
     2   - CDLC * (RMIXL(TXOP,P0) - RMIXL(TSOP,P0) * 0.8) 
C       SNOWFALL
     3   - PWLF * SN
  
C       COOLING DUE TO RESIDUAL SNOW
C       FROM PREVIOUS STEP
        IF(XIOP.GT.0.) DTX = DTX - PWLF * HOP 
  
C     CALCULATION OF MIXED LAYER HEAT CAPACITY MOVED TO SBR CON 
C     CURRENTLY 120*4.1855E6 (JOULES/M*DEGC). 120M MIXED LAYER. 
  
C       DIVIDE BY THE HEAT CAPACITY OF THE MIXED LAYER
C       AT N-1 TO GIVE THE DERIVATIVE OF TEMPERATURE. 
        DTX = DTX/CMIX
C 
C       EXTRAPOLATION 
        TXO = TXOP + DTX * DELT 
        TM = TXO
        QMO = 0.0 
C 
      ELSE
C       ICE AT CURRENT STEP 
C 
C       SOLVE IMPLICIT EQUATION AT CURRENT STEP FOR TXO.
C 
C       CON IS TERMS FOR SHORT AND LONG WAVE RADIATION
C       INDEPENDENT OF TXO
        CON = (1. - AL) * S5 * SP + XL3 
C 
        CALL IMP (ITMAX, 0.001, 0.001, TXOP, FSEA,
     1T, CON, TSO, P0, HO, XIO) 
C 
        IF (T.LE.273.) THEN 
C 
          TXO = T 
          TM=273. 
          QMO = 0.0 
         ELSE 
C 
          TXO = 273.
          TM=273. 
          QMO = FSEA(273., CON, TSO, P0, HO, XIO) 
C         MELTING NOW 
        ENDIF 
      ENDIF 
  
C     LAND SURFACE
C     ************* SECG ************************ 
  
C     COMPUTES ONE STEP OF THE EQUATIONS FOR THE LAND SURFACE.
C     ICE/SNOW THICKNESS ON LAND
  
C     DERIVATIVE AT N-1 
      DHP = 0.0 
  
C     SNOWFALL
C     MIXING RATIO AFFECT ON LAND SNOW FALL 12/1/79 
      IF(TSLP.LE.273.) DHP=DHP+R(TSLP)
  
      IF(HLP.GT.0.) 
  
C        MELTING
     1   DHP = DHP - QMLP/PWLF
C       SUBLIMATION 
     1  -CDPW * (RMIXI(TXLP,PS) - RMIXI(TSLP,PS) * 0.8) 
  
  
C     EXTRAPOLATION 
      HL = HLP + DHP * DELT 
  
      IF(HL.LT.0.) HL=0.0 
  
C     ALBEDO PARAMETERIZATION 1/16/81 
      IF (HL.EQ.0.0) THEN 
        W(MZ,JALBL)=ALP0L 
       ELSEIF (HL.GT.HLP) THEN
         W(MZ,JALBL)=ALP2 
        ELSE
         W(MZ,JALBL)=ALP3 
      ENDIF 
      AL=W(MZ,JALBL)
  
C     LAND SURFACE TEMPERATURE TXL
  
C     SOLVE IMPLICIT EQUATION AT N FOR TEMP 
  
C     CON IS LONG AND SHORT WAVE HEATING TERMS INDEPENDANT OF TXL.
  
      DP=P0-PS
      IF(DP.GT.0.0)  XL3=XL3*(1.-DL3(DP)) 
      CON = (1 - AL) * S5 * SP + XL3
  
      CALL IMP (ITMAX, 0.001, 0.001, TXLP, FLAND, 
     1  T, CON, TSL, PS, HL, DUMMY) 
C 
      IF((T.LE.273.).OR.(HL.LE.0.)) THEN
C       NO MELTING
        QML = 0.0 
        TXL = T 
       ELSE 
C       MELTING 
        TXL = 273.
        QML = FLAND(273., CON, TSL, PS, HL, DUMMY)
      ENDIF 
  
      IF(JAVE.NE.JAVEON) GO TO 142
      IF((MZ.LT.MZERO).OR.(MZ.GE.(MZERO+LHLS))) GO TO 142 
      CON =(1.-ALP2)*S5*SP+XL3
      TZ  =(CON/SIG)**.25 
      IF(TZ.LE.273.) THEN 
C       NO MELTING IN PERPETUAL ICE CASE
        QMLZ=0.0
       ELSE 
C       MELTING IN PERPETUAL ICE CASE 
        QMLZ=CON-SIG*(273.)**4
      ENDIF 
  
C     NEGLECT SUBLIMATION ,ASSUME SNOWFALL AT A MULTIPLE (FPRSNL) 
C     OF THE USUAL RATE 
C     POTENTIAL ACC SNOW ALL THE YEAR DEC 79,DEC 80 MOD 
      FPRSNL=1.0
      DHLZ=-QMLZ/PWLF+RSNL *FPRSNL
C     ADD DHLZ TO ACCUMULATED SUM 
      W(MZ,JDHLZ)=W(MZ,JDHLZ)+DHLZ
  
 142  CONTINUE
  
  
C     ******************************************* 
  
      W(MZ,JTH1 )=TH1 
      W(MZ,JTH2 )=TH2 
      W(MZ,JTSL )=TSL 
      W(MZ,JTSO )=TSO 
      W(MZ,JTXL )=TXL 
      W(MZ,JTXO )=TXO 
      W(MZ,JTM  )=TM
      W(MZ,JHL  )=HL
      W(MZ,JHO  )=HO
      W(MZ,JXIO )=XIO 
      W(MZ,JQML )=QML 
      W(MZ,JQMO )=QMO 
  
      CALL NOTEMZ(MZ) 
  
 30   CONTINUE
  
  
      RETURN
      END 
