      SUBROUTINE CNSET
C     STATEMENT FUNCTIONS REMOVED OR MODIFIED FOR FTN5 8/5/83.
C     SET UP 2 BY 2 BLOCK TRIDIAGONAL SOLVER TO TREAT 
C     DIFFUSION ALONG PRESSURE SURFACES RATHER THAT SIGMA 
C     SURFACES  CA.3/12/81
C     PARAMETERS XLAMP MPARA ARE DUMMYS ALL INFO IS FROM COMMON 
C       XLAMP,MPARA REMOVED 10/10/83. 
      COMMON/CONST/ 
     1M,MM,MP,MPP,N,
     2DELPHI,DELT,PI,AEARTH,CA, 
C     NOTE NON-STANDARD VARIABLE NAMES DENOTING MILLIBAR UNITS
     3CP,COMB,CD,P00MB,P0MB,
     4SIGMA1,SIGMA2,XKAPPA,SIG,XLC, 
     5XLF,RHOW,RHOS,DIFF,XKS, 
     6XKI,XIMIN,SC, 
     7ITMAX,XLAM,XLS,PWLF,
     8PWLS,CDPW,CDCP,CDLC,CDLS, 
     9PWPS,XKSKI,RSEA,SGKP1,SGKP2,
     1ZSGKPA,ZSGKPB,CLRE,SQ1,SQ2
      COMMON/MINOR/NMINOR,
     1JFM,JRSN,JPS,JSP,JSPP,JCZB,JCZBP, 
     2JQ1P,JQ2P 
     3 ,JEXHT 
     4 ,JFSM(3,2),JTEQ,JXIEQ,JRSNL,JRSOUT,JRLOUT
     5 ,JDHLZ 
     6 ,JHELV 
     7 ,JALV
     8 ,JALBL,JALBO 
      COMMON/TDCOM/DELX,DELX2,TDPHI,PI2 
      COMMON W(46,50) 
      COMMON/WSPACE/GG(46)
      REAL SG(2),Z(2),ALSWP(2),GGO(2),ZSWP(2) 
      REAL U(2),V(2)
      REAL DELTAK(2,2)
      REAL AQT(2,2),SGK(2)
      COMMON/CNCNT/IPSOLD,IPSCNT,MOLD 
      DATA IPSOLD,IPSCNT,MOLD/0,0,0/
C     STATEMENT FUNCTIONS 
C     LAND SURFACE PRESSURE 
      PSL(MS)=W(MS+1,JPS)*SIPERMB 
C     OCEAN SURFACE PRESSURE
C     PSO(MS)=P0SI
C     LAND FRACTION 
      FL(MS)=W(MS+1,JFM)
C     OCEAN FRACTION
      FO(MS)=1.-W(MS+1,JFM) 
C     GAMMAS =((PS/P00SI)*SIGMA)**XKAPPA
C     LAND
      GAML(MS,I)=GG(MS+1)*SGK(I)
C     OCEAN 
C     GAMO(MS,I)=GGO(I) 
C     HOOK FOR ADDING VARIABLE DIFFUSION
C     DF(MS)=DIFF 
C     COS(PHI)
      C(MS)=COS(MS*DELPHI-PI2)
C     G IS COSI*D*COS(PHI)/(DELTA Z)
      GM(MS)=COSI*DIFF*C(MS)*DZI
C     G*P BAR 
      GPBARO(MS)=0.5*(GM(MS)*P0SI+GM(MS+1)*P0SI)
      GPBARL(MS)=0.5*(GM(MS)*PSL(MS)+GM(MS+1)*PSL(MS+1))
C     CHI WITH REVERSED SUBSCRIPTS(6/3/81 MOD)
      CHISWPO(I)=-P0SI*DZ*DELTAK(I,1) 
      CHISWPL(MS,I)=-PSL(MS)*DZ*DELTAK(I,1) 
C     G*CHI BAR 
      GCBARO(MS,I)=0.5*(GM(MS)*CHISWPO(I)+GM(MS+1)*CHISWPO(I))
      GCBARL(MS,I)=0.5*(GM(MS)*CHISWPL(MS,I)+GM(MS+1)*CHISWPL(MS+1,I))
C     SPM,+ FOR ONE,- FOR TWO 
      SPM(I)=SIGN(1.0,1.5-I)
C     FIRST TERM IN ALPHA ,BETA HAT EXPRESSIONS 
      FIRDNL(MS,I)=GPBARL(MS)*ALSWP(I)/DELX 
      FIRDNO(MS,I)=GPBARO(MS)*ALSWP(I)/DELX 
      FIRUPL(MS,I)=GCBARL(MS,I)/DELX
      FIRUPO(MS,I)=GCBARO(MS,I)/DELX
C     SECOND TERM IN ALPHA HAT EXPRESSIONS
      ASECL(MS)=0.5*GM(MS)*(PSL(MS+1)-PSL(MS))/DELX 
C     ASECO(MS)=0.0 
C     SECOND TERM IN BETA HAT EXPRESSIONS 
      BSECL(MS)=0.5*GM(MS+1)*(PSL(MS+1)-PSL(MS))/DELX 
C     BSECO(MS)=0.0 
C     ALPHA HAT 
      AHTDNL(MS,I)=SPM(I)*(-FIRDNL(MS,I)+ASECL(MS)) 
      AHTDNO(MS,I)=SPM(I)*(-FIRDNO(MS,I)+0.0) 
      AHTUPL(MS,I)=SPM(I)*(-FIRUPL(MS,I)+ASECL(MS)) 
      AHTUPO(MS,I)=SPM(I)*(-FIRUPO(MS,I)+0.0) 
C     BETA HAT
      BHTDNL(MS,I)=SPM(I)*(FIRDNL(MS,I)+BSECL(MS))
      BHTDNO(MS,I)=SPM(I)*(FIRDNO(MS,I)+0.0)
      BHTUPL(MS,I)=SPM(I)*(FIRUPL(MS,I)+BSECL(MS))
      BHTUPO(MS,I)=SPM(I)*(FIRUPO(MS,I)+0.0)
C     ALPHA TILDE 
C     UPPER AND LOWER LAYER SUBSCRIPTS
C     IDN(MS)=2 
C     IUP(MS)=1 
C     DIAGONAL J,K TERMS(6/3/81 MOD)
C     I=1 IS J,I=2 IS K 
C     EXPESS THETA LAND IN TERMS OF THETA OCEAN 
C     TH1L=U(1)*TH1O+U(2)*TH2O
C     TH2L=V(1)*TH1O+V(2)*TH2O
      REMIXO(SFO,MS)=FO(MS)*SFO 
      REMIXL(SFL1,SFL2,MS,I)=FL(MS)*(U(I)*SFL1+V(I)*SFL2) 
      REGIXO(SFO,MS,I)=GGO(I)*FO(MS)*SFO
      REGIXL(SFL1,SFL2,MS,I)=FL(MS)*(U(I)*GAML(MS,1)*SFL1 
     1 +V(I)*GAML(MS,2)*SFL2) 
C     J,K HAT TERMS WITH ADDED SIGNS
      DNJKHT(MS,I)=REGIXL(DNJKL1,DNJKL2,MS,I)+REGIXO(DNJKO,MS,I)
      UPJKHT(MS,I)=REGIXL(UPJKL1,UPJKL2,MS,I)+REGIXO(UPJKO,MS,I)
C     GAMMAS IN REGIXL CHANGED 5/27/81
C     END STATEMENT FUNCTIONS 
C     RETURN IF PS,M UNCHANGED OR PS NOT INITIALIZED
      IF((IPSCNT.EQ.IPSOLD.AND.M.EQ.MOLD).OR.IPSCNT.EQ.0)RETURN 
      MOLD=M
      IPSOLD=IPSCNT 
C     SETUP CONVERSION FACTOR FOR PRESSURE
C     SI UNITS(KG/M*SEC**2)PER MILLIBAR 
      SIPERMB=1.E2
      P0SI=SIPERMB*P0MB 
      P00SI=SIPERMB*P00MB 
      COSI=COMB/SIPERMB 
      DELX=AEARTH*DELPHI
      DELX2=DELX*DELX 
      TDPHI=DELPHI
      PI2=PI/2. 
      SG(1)=SIGMA1
      SG(2)=SIGMA2
C     SIGMA**XKAPPA 
      SGK(1)=SGKP1
      SGK(2)=SGKP2
      DO 10 MZ=1,MPP
 10   GG(MZ)=(W(MZ,JPS)/P00MB)**XKAPPA
      DO 20 I=1,2 
      Z(I)=ALOG(SG(I))
      GGO(I)=(P0SI/P00SI)**XKAPPA*SGK(I)
 20   CONTINUE
      DZ=Z(1)-Z(2)
      DZI=1./DZ 
C     Z WITH REVERSED SUBSCRIPTS
      ZSWP(1)=Z(2)
      ZSWP(2)=Z(1)
C     ALPHA WITH REVERSED SUBSCRIPTS(6/3/81 MOD)
      ALSWP(1)=0. 
      ALSWP(2)=DZ 
C     KRONECKER DELTA FUNCTION(6/3/81 MOD)
      DELTAK(1,1)=1.
      DELTAK(2,2)=1.
      DELTAK(1,2)=0.
      DELTAK(2,1)=0.
C     INITIALIZE MATRICES 
C     GEE IS ACCELERATION OF GRAVITY
      GEE=CP/(2.*COSI)
      QSCALE=DELT*(GEE/CP)
      CALL TDSET(M,II,JJ,QSCALE)
C     DIFFUSION OPERATOR
      DO 150 MS=1,M 
C     U AND V FOR EXPRESSING THETA LAND IN TERMS OF THETA OCEAN 
C     TH1L=U(1)*TH1O+U(2)*TH2O
C     TH2L=V(1)*TH1O+V(2)*TH2O
      PLAM=(P0SI/PSL(MS))**XKAPPA 
      Z0HT=ALOG(P0SI/PSL(MS)) 
      U(1)=(DZ-Z0HT)*PLAM/DZ
      V(2)=(DZ+Z0HT)*PLAM/DZ
      U(2)=Z0HT*SGK(2)*PLAM/(SGK(1)*DZ) 
      V(1)=-Z0HT*SGK(1)*PLAM/(SGK(2)*DZ)
      DO 151 I=1,2
C     THE FOLLOWING VARIABLES WERE STATEMENT FUNCTIONS IN THE 
C     FORTRAN 4 VERSIONS OF CNSET.  THEY WERE CHANGED TO COMPLY 
C     WITH FTN 5 RESTRICTIONS ON STATEMENT FUNCTIONS. 8-5-83
      AWDNL2=-GAML(MS-1,2)*AHTDNL(MS-1,2) 
      AWDNL1=-GAML(MS-1,1)*AHTDNL(MS-1,1) 
      AWDNO=-GGO(I)*AHTDNO(MS-1,I)
      AWUPL1=-GAML(MS-1,1)*AHTUPL(MS-1,1) 
      AWUPL2=-GAML(MS-1,2)*AHTUPL(MS-1,2) 
      AWUPO=-GGO(I)*AHTUPO(MS-1,I)
C     BETA TILDE
C     BWDNL=BWDNLX(MS,I)+BWDNLY(MS,I) 
      BWDNLX1=GAML(MS,1)*(AHTDNL(MS,1)) 
      BWDNLY1=GAML(MS,1)*(-BHTDNL(MS-1,1))
      BWDNLX2=GAML(MS,2)*(AHTDNL(MS,2)) 
      BWDNLY2=GAML(MS,2)*(-BHTDNL(MS-1,2))
      BWDNO=GGO(I)*(AHTDNO(MS,I)-BHTDNO(MS-1,I))
C     BWUPL=BWUPLX(MS,I)+BWUPLY(MS,I) 
      BWUPLX1=GAML(MS,1)*(AHTUPL(MS,1)) 
      BWUPLY1=GAML(MS,1)*(-BHTUPL(MS-1,1))
      BWUPLX2=GAML(MS,2)*(AHTUPL(MS,2)) 
      BWUPLY2=GAML(MS,2)*(-BHTUPL(MS-1,2))
      BWUPO=GGO(I)*(AHTUPO(MS,I)-BHTUPO(MS-1,I))
C     DELTA TILDE 
      DWDNL1=GAML(MS+1,1)*BHTDNL(MS,1)
      DWDNL2=GAML(MS+1,2)*BHTDNL(MS,2)
      DWDNO=GGO(I)*BHTDNO(MS,I) 
      DWUPL1=GAML(MS+1,1)*BHTUPL(MS,1)
      DWUPL2=GAML(MS+1,2)*BHTUPL(MS,2)
      DWUPO=GGO(I)*BHTUPO(MS,I) 
C     THIS IS THE END OF THE NEW VARIABLES SECTION. 
      CALL TDM(MS,2,I,REMIXL(AWDNL1,AWDNL2,MS,I)) 
      CALL TDM(MS,2,I,REMIXO(AWDNO,MS)) 
      CALL TDM(MS,1,I,REMIXL(AWUPL1,AWUPL2,MS,I)) 
      CALL TDM(MS,1,I,REMIXO(AWUPO,MS)) 
      CALL TD(MS,2,I,REMIXL(BWDNLX1,BWDNLX2,MS,I))
      CALL TD(MS,2,I,REMIXL(BWDNLY1,BWDNLY2,MS,I))
      CALL TD(MS,2,I,REMIXO(BWDNO,MS))
      CALL TD(MS,1,I,REMIXL(BWUPLX1,BWUPLX2,MS,I))
      CALL TD(MS,1,I,REMIXL(BWUPLY1,BWUPLY2,MS,I))
      CALL TD(MS,1,I,REMIXO(BWUPO,MS))
      CALL TDP(MS,2,I,REMIXL(DWDNL1,DWDNL2,MS,I)) 
      CALL TDP(MS,2,I,REMIXO(DWDNO,MS)) 
      CALL TDP(MS,1,I,REMIXL(DWUPL1,DWUPL2,MS,I)) 
      CALL TDP(MS,1,I,REMIXO(DWUPO,MS)) 
 151  CONTINUE
 150  CONTINUE
C     SCALE BY 2 BY 2 MATRIX INVERSE TO UNSCRAMBLE TIME DERIVATIVES 
      DO 160 MS=1,M 
C     U AND V FOR EXPRESSING THETA LAND IN TERMS OF THETA OCEAN 
C     TH1L=U(1)*TH1O+U(2)*TH2O
C     TH2L=V(1)*TH1O+V(2)*TH2O
      PLAM=(P0SI/PSL(MS))**XKAPPA 
      Z0HT=ALOG(P0SI/PSL(MS)) 
      U(1)=(DZ-Z0HT)*PLAM/DZ
      V(2)=(DZ+Z0HT)*PLAM/DZ
      U(2)=Z0HT*SGK(2)*PLAM/(SGK(1)*DZ) 
      V(1)=-Z0HT*SGK(1)*PLAM/(SGK(2)*DZ)
      DO 161 I=1,2
      DNJKO=0.5*P0SI*DELTAK(I,2)
      DNJKL1=0.5*PSL(MS)*DELTAK(1,2)
      DNJKL2=0.5*PSL(MS)*DELTAK(2,2)
      UPJKO=0.5*P0SI*DELTAK(I,1)
      UPJKL1=0.5*PSL(MS)*DELTAK(1,1)
      UPJKL2=0.5*PSL(MS)*DELTAK(2,1)
      AQT(2,I)=DNJKHT(MS,I) 
 161  AQT(1,I)=UPJKHT(MS,I) 
      CALL TDSC(MS,AQT) 
 160  CONTINUE
C     CALL TDEND TO TIDY UP 
      SCALE=-(GEE/CP)*(DELT/DELX) 
      CALL TDEND(II,JJ,KK,SCALE)
C     CALL CNBTRI TO INITIALIZE BLOCK TRIDIAGONAL SOLVER
      CALL CNBTRI 
      RETURN
      END 
