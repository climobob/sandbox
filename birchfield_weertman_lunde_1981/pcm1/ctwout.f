      SUBROUTINE CTWOUT(NS,NSZ,NSCPR,MSAPR,MSBPR,MSCPR,IOUTL,IOUTM) 
C     CTWOUT ENTERS DATA INTO CARIN TO BE COMPRESSED BY SQADD.
C     THE SPECIFICATION IS MADE IN THE DOUBLE PRECISION NUMBERS 
C     PASSED IN.  A ONE IN THE NTH PLACE MEANS THAT W(MZ,N) IS
C     TO BE ENTERED.  A ZERO MEANS DO NOTHING.
C     LATITUDE LIMITS AND SPACING ARE GIVEN BY THE 3 MS PARAMETERS
C     NS IS CURRENT TIME.NSZ IS THE INITIAL CONDITION 
C     NSCPR IS THE TIME SKIPING INTERVAL.ALL THESE ARE ON THE SAME
C     TIME SCALE .
C     THIS ALSO PRINTS (LABEL) THE FLAGS SET BY NOTEMZ AND CLEARS THEM
C     BE CAREFUL IN MAKING MODIFICATIONS
C     REWRITTEN FOR MACHINE INDEPENDANCE 8/9/83.  BOB GRUMBINE
      CHARACTER*3 LABEL(3)
      DOUBLE PRECISION IOUTL,IOUTM,NUMBER 
      CHARACTER CARIN*440,CAROUT*240
      COMMON W(46,50) 
      COMMON/NOTETW/NTW(46,3),JNT 
      COMMON/LARGE/NFLD,JFLD(12,2)
      COMMON/MINOR/NMINOR,KFLD(26)
      DATA LABEL/' TB',' CA',' IM'/ 
      DATA IUNIT/6/ 
C     STATEMENT FN. TO RETURN THE DIGIT IN THE NTH PLACE
C     USE 1.D1 RATHER THAN 10 TO GET A DOUBLE PRECISION RESULT. 
      KOUT(N,NUMBER)=DINT(10*((NUMBER/1.D1**N)-DINT(NUMBER/1.D1**N))) 
      IF (MOD(NS-NSZ  ,NSCPR).NE.0) RETURN
      WRITE(IUNIT,30) NS
 30   FORMAT(9H TIME NS=,I9)
      DO 10 MS=MSAPR,MSBPR,MSCPR
      CARIN=' ' 
      CAROUT=' '
      LENIN=1 
      MZ=MS+1 
      WRITE (CARIN(1:11),'(1X,I9)') MS
      LENIN=LENIN+11
      NUMBER=IOUTL
      DO 40 L=1,NFLD
      IF (KOUT(L,NUMBER).NE.0) THEN 
        JJSUBA=JFLD(L,2)
        WRITE (CARIN(LENIN:LENIN+14),'(G14.7)') W(MZ,JJSUBA)
        LENIN=LENIN+15
      ENDIF 
 40   CONTINUE
C     OPTIONAL THBAR,THHAT COMPUTATION       9/7/79 
      JJSUBA=JFLD(1,2)
      JJSUBB=JFLD(2,2)
      IF (KOUT(13,NUMBER).NE.0) THEN
        THBAR=(W(MZ,JJSUBA)+W(MZ,JJSUBB))*0.5 
        WRITE (CARIN(LENIN:LENIN+14),'(G14.7)') THBAR 
        LENIN=LENIN+15
      ENDIF 
      IF (KOUT(14,NUMBER).NE.0) THEN
        THHAT=(W(MZ,JJSUBA)-W(MZ,JJSUBB))*0.5 
        WRITE (CARIN(LENIN:LENIN+14),'(G14.7)') THHAT 
        LENIN=LENIN+15
      ENDIF 
      NUMBER=IOUTM
      DO 50 L=1,NMINOR
      IF (KOUT(L,NUMBER).NE.0) THEN 
        JJSUBA=KFLD(L)
        WRITE (CARIN(LENIN:LENIN+14),'(G14.7)') W(MZ,JJSUBA)
        LENIN=LENIN+15
      ENDIF 
 50   CONTINUE
      DO 70 L=1,JNT 
      IF (NTW(MZ,L).NE.0) THEN
          WRITE (CARIN(LENIN:LENIN+3),'(A3)') LABEL(L)
          LENIN=LENIN+4 
      ENDIF 
 70   CONTINUE
C     NOW CALL SQADD TO REMOVE REPEATED BLANKS IN THE FIELD.
      CALL SQADD(CARIN,CAROUT,LENIN,LENOUT) 
      IWIDTH=136
      IF (LENOUT.GT.IWIDTH) THEN
        WRITE (6,32) CAROUT(1:IWIDTH) 
        WRITE (6,32) CAROUT(IWIDTH+1:LENOUT)
      ELSE
        WRITE (6,32) CAROUT(1:LENOUT) 
      ENDIF 
C     THE A FORMAT WITH NO LENGTH SPECIFIED IS PRINTED OUT LEFT-
C     JUSTIFIED TO ITS LENGTH BY DEFAULT. 
C     IWIDTH SPECIFIES THE PAGE WIDTH.  THIS IS USED TO AVOID 
C     PROBLEMS WITH A MAXIMUM RECORD SIZE.  IT ALLOWS FOR 
C     WORKING ON DIFFERENT MACHINES BY CHANGING IWIDTH. 
 32   FORMAT (1X,A) 
 10   CONTINUE
      DO 71 L=1,3 
      DO 60 MZK=1,46
      NTW(MZK,L)=0
 60   CONTINUE
 71   CONTINUE
      RETURN
      END 
