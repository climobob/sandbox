       SUBROUTINE ASTR(PARA)
C      CONVERT INSOL MAIN TO ASRT SUBROUTINE   8/21/79
       REAL AE(19),BE(19),CE(19), 
     1 AOB(47),BOB(47),COB(47), 
     2 AOP(78),BOP(78),COP(78)
       COMMON/NORBIT/AT,AXOB,AECC,APRE,APERH
       DATA K/5 / 
C      ASTRONOMICAL TIME IN YEARS(PAST TIME NEGATIVE) 
       T=PARA 
       GO TO 900
C      ENTRY TO READ DATA CARDS AND INITIALIZE
       ENTRY ASTSET 
       PI=3.1415926535898 
       PIR=PI/180.
       PIRR=PIR/3600. 
C 
C      READ AMPLITUDE A MEAN RATE B PHASEC
C      FOR TERMS IN TRIG SERIES. THEY ARE 
C      CONVERTED TO RADIANS IMMEDIATELY 
C      NEF NOB NOP NUMBER OF TERMS READ IN
C 
C      USE K AS A UNIT NUMBER FOR INPUT 
C      ECCENTRICITY 
       READ(K,200,END=9000 )NEF 
  200  FORMAT(BZ,I3)
  201  FORMAT(BZ,5X,3F15.10)
 9000  DO 1 I=1,NEF 
       READ(K,201,END=9001 )AE(I),Y,Z 
 9001  BE(I)=Y*PIRR 
       CE(I)=Z*PIR
    1  CONTINUE 
C 
C      OBLIQUITY
       READ(K,200,END=9002 )NOB 
 9002  XOD=23.320556
       DO 3 I=1,NOB 
       READ(K,201,END=9003 )AOB(I),Y,Z
 9003  BOB(I)=Y*PIRR
       COB(I)=Z*PIR 
    3  CONTINUE 
C 
C      GENERAL PRECESSION IN LONGITUDE
       XOP=3.392506 
       PRM=50.439373
       READ(K,200,END=9004 )NOP 
 9004  DO 31 I=1,NOP
       READ(K,201,END=9005 )AOP(I),Y,Z
 9005  BOP(I)=Y*PIRR
       COP(I)=Z*PIR 
   31  CONTINUE 
C      NEFF NOBB NOPP NUMBER OF TERMS KEPT FOR COMPUTATION. 
C      THE NUMBER OF TERMS KEPT FOR COMPUTATION IS SET EQUAL
C      TO THE NUMBER OF TERMS READ IN 
       NEFF=NEF 
       NOBB=NOB 
       NOPP=NOP 
       RETURN 
C 
C      COMPUTATION OF ORBITAL PARAMETERS
 900   CONTINUE 
C 
C      COMPUTE SERIES FOR ECC ECCENTRICITY TIMES SIN AND COS
       XES=0.00 
       XEC=0.00 
       I=NEFF+1 
       DO 4 IB=1,NEFF 
       I=I-1
       ARG=BE(I)*T+CE(I)
       XES=XES+AE(I)*SIN(ARG) 
       XEC=XEC+AE(I)*COS(ARG) 
    4  CONTINUE 
       ECC=SQRT(XES*XES+XEC*XEC)
C 
C      RP IS LONGITUDE OF PERHELION MEASURED
C      FROM THE EQUINOX OF REFERENCE (FIXED)
C      NORMALIZED TO 0 TO 360 DEGREES 
       IF (ECC.GT.1.0E-6) GO TO 203 
       RP=0.0 
       GO TO 204
  203  RP=-ATAN2(XES,-XEC)+PI 
C 
C      PRG IS GENERAL PRECESSION IN LONGITUDE 
  204  PRG=PRM*T
       I=NOPP+1 
       DO 5 IB =1,NOPP
       I=I-1
       PRG=PRG+AOP(I)*SIN(BOP(I)*T +COP(I)) 
    5  CONTINUE 
       PRG=PRG/3600.+XOP
C 
C       PERH IS LONGITUDE OF PERIHELION 
C      MEASURED FROM THE MOVING AUTUMNAL EQUINOX
C      NORMALIZED TO 0 TO 360 DEGREES 
C      (ANDRE BERGER REFERS TO HIS VARIABLES AS MEASURED FROM 
C      THE VERNAL EQUINOX BUT IN THE PROGRAM USES VALUES 180
C      DEGREES AWAY FROM THIS. THIS CAN CAUSE CONFUSION. )
       PERH=RP/PIR+PRG
   54  IF (PERH) 51,55,53 
   51  PERH=PERH+360. 
       GO TO 54 
   53  IF (PERH.LT.360) GO TO 55
       PERH=PERH-360
       GO TO 53 
   55  CONTINUE 
C 
C      PRE IS PRECESSIONAL PARAMETER
       PRE=ECC*SIN(PERH*PIR)
C 
C      XOB IS OBLIQUITY 
       XOB=XOD
       I=NOBB+1 
       DO 6 IB=1,NOBB 
       I=I-1
       XOB=XOB+AOB(I)/3600.*COS(BOB(I)*T+COB(I))
    6  CONTINUE 
C      TRANSFER RESULTS TO COMMON/NORBIT/ 
       AT=T 
       AXOB=XOB 
       AECC=ECC 
       APRE=PRE 
       APERH=PERH 
       RETURN 
       END
