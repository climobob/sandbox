

      SUBROUTINE YEAR(KPHI,KYEAR,WW,COSZB,JP) 
C     JP IS POSITION IN YEAR (1 TO KYEAR) 
C     KPHI=MPP,KYEAR=NCG
C     CALL YRESET TO COMPUTE A YEARS
C     CYCLE AND WRITE IT ON TAPE 15 IN
C     A RANDOM FILE   JJ=1 IS THE VERNAL EQUINOX
      REAL WW(KPHI),COSZB(KPHI) 
      DIMENSION INDEX(251)
      COMMON/CFCOM/NCG,NSC
      COMMON/NORBIT/DUM,XOB,ECC,PRE,PERH
C     XOB OBLIQUITY 
C     ECC ECCENTRICITY
C     PRE ECC*SIN(PERH) = PRECESSIONAL PARAMETER
C     PERH LONGITUDE OF PERIHELION
C     PI AND SOLAR CONSTANT FROM
C     COMMON/CONST/ 
      COMMON/CONST/DMA(7),PI,DMB(19),SS,DMC(19) 
      DATA IXSZ15 /251/ 
      DATA IRW/0/ 
C     MOD MODIFIED TO WORK FOR NEG. JP . GIVES  1 TO KYEAR   .
      JM=1+MOD(KYEAR+MOD(JP-1,KYEAR),KYEAR) 
      CALL READMS(15,WW,KPHI,JM)
      CALL READMS(15,COSZB,KPHI,JM+KYEAR) 
C     ALL COMPUTATION IN YEAR IS ON THE FINE GRID SCALE AND 
C     ARRAY SIZES EXCEPT FOR THIS COMPRESSION   OF MS 
C     MODIFIED TO RUN ONLY ON COARSE GRID 8/24/83.
      J=0 
      DO 300 I=1,KPHI 
      J=J+1 
      WW(J)=WW(I) 
      COSZB(J)=COSZB(I) 
  300 CONTINUE
      RETURN
      ENTRY YRSET(KPHI,KYEAR,WW,COSZB,JP) 
C     OPEN RANDOM FILE ON TAPE 15 IF NOT DONE BEFORE
      IF (IRW.EQ.0) CALL OPENMS(15,INDEX,IXSZ15,0)
      TEST=0.0001 
      PIR=PI/180. 
C     INSOLATION WILL BE IN SAME UNITS AS SOLAR CONSTANT, RATHER THAN 
C     DAILY,AS IN BERGERS PROGRAM, BECAUSE THE CONVERSION FACTOR TAU
C     (TAU=86.4(K SEC/DAY)) IS OMITTED. 
      SF=SS/PI
C     SO SINE OF OBLIQUITY
      SO=SIN(XOB*PIR) 
C     XL LONGITUDE OF PERIHELION + 180 DEGREES
      XL=PERH+180.
      XLLP=XL*PIR 
C     COMPUTE MEAN LONGITUDE AT THE VERNAL EQUINOX
      XEE=ECC*ECC 
      XSE=SQRT(1.-XEE)
      XLAM=ECC*(0.5+XEE/8.)*(1.+XSE)*SIN(XLLP)
     1 -XEE/4.*(.5+XSE)*SIN(2.*XLLP)
     2 +ECC*XEE/8*(1./3.+XSE)*SIN(3.*XLLP)
      XLAM=2.*XLAM/PIR
C     INCREMENT FOR MEAN ANOMALY - ALM
      ALMINC=360./KYEAR 
C     INCREMENT FOR PHI 
      PHINC=180./(KPHI-1.)
C     START TIME AT VERNAL EQUINOX
      ALM=XLAM-XL-ALMINC
      DO 100 JJ=1,KYEAR 
      ALM=ALM+ALMINC
      RANM=ALM*PIR
C     FIND TRUE ANOMALY - RANV
      RANV=RANM+ECC*(2.-XEE/4)*SIN(RANM)
     1 +5./4.*XEE*SIN(2.*RANM)
     2 +13./12.*ECC*XEE*SIN(3.*RANM)
      ANV=RANV/PIR
C     TRUE LONGITUDE OF THE SUN - DLAM
      DLAM=ANV+XL 
      RLAM=DLAM*PIR 
C     EARTH-SUN DISTANCE RAU
      RAU=(1.-XEE)/(1.+ECC*COS(RANV)) 
      RSQIN=1./(RAU*RAU)
      RSQPIN=1./(PI*RAU*RAU)
      S=SF/(RAU*RAU)
C     COMPUTE SIN,COS OF DECLINATION OF SUN 
      SD=SO*SIN(RLAM) 
      CD=SQRT(1-SD*SD)
C     DELTA IS DECLINATION OF SUN 
      RDELTA=ATAN(SD/CD)
      DELTA=RDELTA/PIR
      ADELTA=ABS(DELTA) 
C     START PHI AT -90 DEGREES
      PHI=-90.-PHINC
      DO 200 II = 1, KPHI 
      PHI=PHI+PHINC 
      RPHI=PHI*PIR
      SP=SD*SIN(RPHI) 
      CP=CD*COS(RPHI) 
      APHI=ABS(PHI) 
      TT=ABS(APHI-90.)
C     SINGULARITY FOR APHI=90 AND DELTA=0 
C     PARTICULAR CASES FOR PHI=0 OR DELTA=0 
      IF ((TT.LE.TEST).AND.(ADELTA.LE.TEST)) GO TO 2
      IF (ADELTA.LE.TEST) GO TO 6 
      IF (APHI.LE.TEST) GO TO 7 
      AT=90-ADELTA
      SPD=PHI*DELTA 
      IF (APHI.LT.AT) GO TO 3 
      IF (SPD) 2,3,4
C     POLAR CONTINUAL NIGHT 
  2   WW(II)=0.0
C     THE VALUE OF COSZB IS ARBITRARY 
      COSZB(II)=0.0 
      GO TO 200 
C     POLAR CONTINUAL DAY 
  4   WW(II)=S*SP*PI
      COSZB(II)=SP*RSQIN
      GO TO 200 
C     DAILY SUNRISE AND SUNSET
  3   TP=-SP/CP 
      STP=SQRT(1.-TP*TP)
      RDAYL=ACOS(TP)
      WW(II)=S*(RDAYL*SP+CP*STP)
      COSZB(II)=RSQIN*(SP+CP*STP/RDAYL) 
      GO TO 200 
C     EQUINOXES 
  6   WW(II)=S*COS(RPHI)
      COSZB(II)=2.0*RSQPIN*COS(RPHI)
      GO TO 200 
C     EQUATOR 
  7   WW(II)=S*COS(RDELTA)
      COSZB(II)=2.0*RSQPIN*COS(RDELTA)
  200 CONTINUE
      CALL WRITMS(15,WW,KPHI,JJ,IRW,0)
      CALL WRITMS(15,COSZB,KPHI,JJ+KYEAR,IRW,0) 
  100 CONTINUE
      IRW=-1
C     REWRITE IN PLACE IF POSSIBLE ON NEXT YRSET CALL 
      RETURN
      END 
