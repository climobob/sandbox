      SUBROUTINE GEOSTR
     1(PS,T1,Z1,PSIGM,CHIGM,PSI,CHI,WORK,COSL,IMAX,JMAX,MAXWV,IROMB)
C
C  COMPUTATION OF THE GEOSTROPHIC STREAMFUNCTION PSI AND POTENTIAL CHI
C  AT THE EARTH'S SURFACE (OR ANY OTHER GLOBAL SURFACE - SEE NOTE 1)
C  WHERE
C
C  DEL2(PSI) = DEL2(Z1) + DIV(RD/G*T1*DEL(PS))
C
C  DEL2(CHI) = -DEL X (RD/G*T1*DEL(PS)) = -DEL(RD/G*T1) X DEL(PS)
C
C  INPUT
C     PS      2*NMMAX WAVE COEFS OF LOG SURFACE PRESSURE IN KPA
C     T1      2*NMMAX WAVE COEFS OF BOTTOM SIGMA LAYER TEMPERATURE
C     Z1      2*NMMAX WAVE COEFS OF BOTTOM SIGMA LAYER HEIGHT
C     PSIGM   SPECIFIED GLOBAL MEAN OF PSI
C     CHIGM   SPECIFIED GLOBAL MEAN OF CHI
C     COSL    JMAX COSINES OF LATITUDE
C     IMAX    NUMBER OF GAUSSIAN LONGITUDES
C     JMAX    NUMBER OF GAUSSIAN LATITUDES
C     MAXWV   SPECTRAL TRUNCATION
C     IROMB   =1 FOR RHOMBOIDAL TRUNCATION: NMMAX=(MAXWV+1)*(MAXWV+1)
C             =0 FOR TRIANGULAR TRUNCATION: NMMAX=(MAXWV+1)*(MAXWV+2)/2
C
C  OUTPUT
C     PSI     2*NMMAX WAVE COEFS OF THE GEOSTROPHIC STREAMFUNCTION
C     CHI     2*NMMAX WAVE COEFS OF THE GEOSTROPHIC POTENTIAL
C
C  WORKSPACE
C     WORK    IMAX*JMAX*3 WORDS
C
C  NOTE 1: PSI AND CHI CAN BE COMPUTED ON ANY OTHER GLOBAL SURFACE IF
C          PS, T1 AND Z1 ARE RESPECTIVELY THE LOG PRESSURE, TEMPERATURE
C          AND HEIGHT FIELDS DEFINED ON THAT SURFACE.
C
C  NOTE 2: LIKE THE GEOPOTENTIAL HEIGHT ON A PRESSURE SURFACE,
C          PSI AND CHI HAVE UNITS IN METERS.  THE GEOSTROPHIC WIND IS
C          F*V = K X DEL(PSI) + DEL(CHI).
C
      REAL PS(2,(MAXWV+1)*(MAXWV+2)), T1(2,(MAXWV+1)*(MAXWV+2))
      REAL Z1(2,(MAXWV+1)*(MAXWV+2))
      DIMENSION PSI(2,(MAXWV+1)*(MAXWV+2)), CHI(2,(MAXWV+1)*(MAXWV+2))
      DIMENSION WORK(IMAX,JMAX,3)
      DIMENSION COSL(JMAX)
      DATA SQRT2/1.414214/, RD/287.04/, G/9.8062/
C***********************************************************************
C  TRANSFORM DEL(PS) AND T1 TO GRIDSPACE
      CALL SPHERD
     1(-1,WORK(1,1,1),WORK(1,1,2),PM,PS,WORK(1,1,3),COSL,
     2 IMAX,JMAX,MAXWV,IROMB)
      CALL SPHERT
     1(-1,WORK(1,1,3),T1,0,0,
     2 IMAX,JMAX,MAXWV,IROMB)
 
C  MULTIPLY NONLINEAR TERMS IN GRIDSPACE
      DO 10 J=1,JMAX
      DO 10 I=1,IMAX
        WORK(I,J,1) = RD/G*WORK(I,J,3)*WORK(I,J,1)
        WORK(I,J,2) = RD/G*WORK(I,J,3)*WORK(I,J,2)
   10 CONTINUE
 
C  TRANSFORM BACK TO WAVESPACE, TAKING DIV (PSI) AND CURL (CHI)
      CALL SPHERV
     1(1,WORK(1,1,1),WORK(1,1,2),PSI,CHI,WORK(1,1,3),COSL,
     2 IMAX,JMAX,MAXWV,IROMB)
 
C  TAKE INVERSE LAPLACIAN OF BOTH FIELDS
      CALL LAPLAC
     1(-1,PSI,PSI,MAXWV,IROMB)
      CALL LAPLAC
     1(-1,CHI,CHI,MAXWV,IROMB)
 
C  ADD HEIGHT COMPONENT TO PSI AND REVERSE SIGN IN CHI
      NM = 0
      DO 20 M=0,MAXWV
      DO 20 N=M,MAXWV+IROMB*M
        NM = NM + 1
        PSI(1,NM) = Z1(1,NM) + PSI(1,NM)
        PSI(2,NM) = Z1(2,NM) + PSI(2,NM)
        CHI(1,NM) = -CHI(1,NM)
        CHI(2,NM) = -CHI(2,NM)
   20 CONTINUE
 
C  FIX 0,0 COMPONENT
      PSI(1,1) = PSIGM*SQRT2
      PSI(2,1) = 0.
      CHI(1,1) = CHIGM*SQRT2
      CHI(2,1) = 0.
 
      RETURN
      END
